bind ESCAPE [
	do [
		@nokeybind [ if (|| (cs_interrupt) (hideui "main")) [] [showmain] ]
	]
]

bind I [
	if (&& (= $playermenu 1) (hideui "player")) [] [
		showplayer
		playermenu = 1
		LoadPGInventory player
	]
]

bind C [
	if (&& (= $playermenu 0) (hideui "player")) [] [
		showplayer
		playermenu = 0
		LoadPGStats player
	]
]

newentgui teledest [Yaw Tag] [0 360 0 0xFFFF]
newentgui jumppad [Z Y X Radius] [-500 500 -500 500 -500 500 0 256]
newentgui checkpoint [Yaw] [0 360]
newentgui spawn [Yaw Radius Tag] [0 360 0 256 0 0xFFFF]
newentgui location [Tag Radius] [0 0xFFFF 0 256]
newentgui blip [] []
newentgui camera [Tag Yaw Pitch Roll] [0 0xFFFF 0 360 -180 180 -180 180]
newentgui platformroute [Tag] [0 0xFFFF]

newspawnentgui = [
	alias [GenQuickEnt@arg1] [
		local path props limits
		path = [@@arg2]
		props = [@@arg3]
		limits = [@@arg4]

		uivlist 0.01 [
			uitable 2 0.01 [
				uialign 0 0
				newlims = ""
				looplist l $limits [
					newlims = (concat $newlims (l))
				]
				iterprops $props $newlims 0
				uitext "id" 1 0 [ uialign -1 0 ]

				id = (entgetinfo)
				UIField id 63 [ spawnname $id ] 1 $UISafeFilter
			]

			uitable 6 .01 [
				recursefiles (datapath $path) "cfg" [
					file = (substr $file @(+ 1 (strlen (datapath $path))))
					UIBasicButton [
						uialign 0 0
						uicolor 0x2F7FBFFF .13 .13 [
							uimodelpreview @(escape (getentmodel (enttype) $file)) @(findanims "idle") "" .12 .12
							uitext $file 1 0 [ uialign 0 1]
						]
					] [
						spawnname @(escape $file)
						LoadQuickEdit [main]
					] [
						UITooltip [
							uitext @(escape $file) .9 .4
						]
					]
				]
			]
		]
	]
]

newspawnentgui critter "critters" [Yaw] [0 360]
newspawnentgui obstacle "obstacles" [Yaw] [0 360]
newspawnentgui container "containers" [Yaw] [0 360]
newspawnentgui platform "platforms" [Yaw] [0 360]
newspawnentgui trigger "triggers" [Yaw] [0 360]
newspawnentgui item "items" [Yaw Amount] [0 360 0 0xFFFF]

crosshairs = "default edit"

slots = ["Left hand" "Right hand" "Legs" "Arms" "Torso" "Head" "Feet" "Quiver"]
stats = [Strength Endurance Agility Charisma Wisdom Intelligence Luck]
lstats = [strength endurance agility charisma wisdom intelligence luck]
sstats = [Str End Agi Chr Wis Int Lk]
skills = [Armour Diplomacy Magic Marksman Melee Stealth Craft]
lskills = [armour diplomacy magic marksman melee stealth craft]
sskills = [Arm Dip Mag Mark Mel St Cr]
elements = [ "Fire" "Water" "Air" "Earth" "Arcane" "Psionic" "Divine" "Darkness" "Slash" "Blunt" "Pierce" ]
targets = "Single Multiple Area self Self-Multi Self-Area Horiz-Slash Vert-Slash Cone"
resv_ammo = [ "mana" "health" "experience" ]
resv_ammo_calls = [ "mana" "health" "base_experience" ]

statusicons = [
	"base/health"
	"base/mana"
	"base/scale"
	"base/move"
	"base/weight"
	"base/crit"
	"base/hregen"
	"base/mregen"
	"base/strength"
	"base/endurance"
	"base/agility"
	"base/charisma"
	"base/wisdom"
	"base/intelligence"
	"base/luck"
	"base/armour"
	"base/diplomacy"
	"base/magic"
	"base/marksman"
	"base/melee"
	"base/stealth"
	"base/craft"

	"base/fire_t"
	"base/water_t"
	"base/air_t"
	"base/earth_t"
	"base/arcane_t"
	"base/mind_t"
	"base/holy_t"
	"base/darkness_t"
	"base/slash_t"
	"base/blunt_t"
	"base/pierce_t"

	"base/fire_r"
	"base/water_r"
	"base/air_r"
	"base/earth_r"
	"base/arcane_r"
	"base/mind_r"
	"base/holy_r"
	"base/darkness_r"
	"base/slash_r"
	"base/blunt_r"
	"base/pierce_r"

	"base/stun"
	"base/silence"
	"base/doom"
	"base/lock"
	"base/magelock"
	"base/dispel"
	"base/reflect"
	"base/invis"
	"base/light"
	"base/polymorph"
	"base/signal"
	"base/script"
]

statusdecs = [
	"Affects your current health pool over time. Adversely or otherwise."
	"Affects your current mana pool. Adversely or otherwise."
	"Your size has been affected; Small as a mouse, or large as a house."
	"Affects your movespeed, and your jump height by a larger degree."
	"Your luggage has become lighter and more manageable or heavy as lead."
	"Affects your chances of scoring devastating critical hits."
	"Affects the rate at which your health regenerates naturally over time."
	"Affects the rate at which your mana regenerates naturally over time."
	"Affects your Strength statistic, granting you the strength of a bull, or cursing you with that of a rodent."
	"Affects your Endurance statistic, will you have the blessing of good health, or the curse of a sickly child?"
	"Affects your Agility statistic, grating you either nimble hands or cursing you with oafishness."
	"Affects your Charisma statistic."
	"Affects your Wisdom statistic."
	"Affects your Intelligence statistic."
	"Affects your Luck statistic."
	"Affects your rank in the Armour skill."
	"Affects your rank in the Diplomacy skill."
	"Affects your rank in the Magic skill."
	"Affects your rank in the Marksman skill."
	"Affects your rank in the Melee skill."
	"Affects your rank in the Stealth skill."
	"Affects your rank in the Craft skill."

	"Affects your ability to shrug off Fire damage."
	"Affects your ability to shrug off Water damage."
	"Affects your ability to shrug off Air damage."
	"Affects your ability to shrug off Earth damage."
	"Affects your ability to shrug off Arcane energies."
	"Affects your ability to shrug off Psionic energies."
	"Affects your ability to shrug off Divine energies."
	"Affects your ability to shrug off Dark energies."
	"Affects your ability to shrug off blows from Slashing weaponry."
	"Affects your ability to shrug off blows from Blunt weaponry."
	"Affects your ability to shrug off blows from Piercing weaponry."

	"Affects your defense from Fire."
	"Affects your defense from Water."
	"Affects your defense from Air."
	"Affects your defense from Earth."
	"Affects your defense from the Arcane."
	"Affects your defense from Psionic energies."
	"Affects your defense from Divine magics."
	"Affects your defense from Dark energies."
	"Affects your defenses from Slashing weaponry."
	"Affects your defenses from Blunt weaponry."
	"Affects your defenses from Piercing weaponry."

	"You have been stunned, you are unable to move or use items with a somatic component."
	"You have been silenced and cannot cast spells with a vocal component."
	"Your biological clock is ticking... tick... tick... tick..."
	"Your being is being tightened together and sealed... As least that would be the case if you were a lock!"
	"You have been temporarily sealed by mystic energies... As least that would be the case if you were a lock!"
	"The magical aura around you diminishes and grows weaker."
	"Projectiles literally bounce off of you!"
	"Bends light around you, making it difficult to discern you."
	"Light radiates from you into the surrounding area..."
	"You've been polymorphed into something else! You'll probably have a new perspective of life after this experience!"
	"It's a secret, no really, we don't know what's it's doing either!"
	"It's a secret, no really, we don't know what's it's doing either!"
]

UIMenu "status" [
	UIVScroller .55 .75 .02 .5 [
		uialign -1 -1
		uivlist 0 [
			uicolortext "This is a debug menu, which serves no practical purpose" 2 .5 0xFF0000
			loop i $STATUS_MAX [
				uibutton [] [
					UITooltip [
						uihlist 0 [
							uiimage (r_iconpath (at $statusicons $i)) .1 .1
							uitext (at $statusdecs $i) .8 .4
						]
					]
					loop j 3 [
						uifill .5 .1 [
							uihlist 0 [
								uiimage (r_iconpath "empty") .1 .1 [
									uiimage (r_iconpath (at $statusicons $i)) .1 .1
								]
								uitext (at $statusdecs $i) .8 .4
							]
						]
					]
				]
			]
		]
	]
]

UIMenu "main" [
	uihlist 0.01 [
		uivlist 0.01 [
			if $mainmenu [
				UIButton "New Game" [
					LoadNewGame main
				]
			] [
				UIButton "Abort Game" [
					localdisconnect
					hideui "main"
				]
				uioffset 0 0.02
				UIButton "Save Game" [
					LoadSGame main
				]
			]
			UIButton "Load Game" [
				LoadLGame main
			]
			UICondButton "Editing" [ $editing ] [
				LoadEditing main
			] [
				UITooltip [
					uicond [ $editing ] [
						uitext "Modify map properties and variables, set textures, spawn entties and other madness!" .9 .45
						uitext "You need to be in editmode to access this menu (F1)." .9 .45
					]
				]
			]
			UIButton "Options" [
				LoadOptions main
			]
			UIButton "Credits" [
				LoadCredits main
			]
			uispace 0 0.025
			UIButton "Exit" [ quit ]
		]
		uifill 0.9 0.8 [
			uiclip 0.9 0.8 [
				uitag content [
					uivlist 0 [
						uitext (concat "Welcome to Lamiae" $version) 2
					]
				]
			]
		]
	]
] "Main Menu - Welcome"

gamesmenu = 0
LoadNewGame = [
	replaceui main title [
		uitext "Main Menu - New Game" 1.5
	]
	replaceui $arg1 content [
		uivlist 0.01 [
			uifill 0 0
			uihlist 0.01 [
				UITab gamesmenu 0 "Games" [
					LoadGamesAll [@@@@@arg1]
				]
				UITab gamesmenu 1 "Maps" [
					LoadMapAll [@@@@@arg1]
				]
			]
			UIVScroller .9 .75 0.025 .5 [
				uitag maps []
			]
		]
	]
	gamesmenu = 0
	LoadGamesAll [@arg1]
]

LoadMapAll = [
	replaceui [@arg1] maps [
		uivlist 0.005 [
			uialign -1 0
			loopdir d games [
				local empty
				empty = 1;
				uitext $d 1.25 0
				uitable 6 .01 [
					loopfiles f [games/@@[d]/maps] ogz [
						empty = 0
						UIBasicButton [
							UIBorder [
								UIThumbnail [games/@[d]/maps/@[f]] .125 .125
								uiclip .125 .125 [
									uialign 0 1
									uitext [@@f]
								]
							]
						] [
							newgame [@@d]
							map [@@f]
						]
					]
				]
				if $empty [
					uitext [@[d] does not have any maps]
				]
			]
		]
	]
]

LoadGamesAll = [
	replaceui [@arg1] maps [
		uialign -1 -1
		uivlist 0.01 [
			loopfiles f games cfg [
				UIBasicButton [
					uiclip .87 .1 [ uifill .87 .1 [
						uihlist .02 [
							uiimage [games/@@@@@[f].png] .1 .1 [
								uialtimage $logo
							]
							uitext [@@@@@f] 1.25 .65
						]
					]]
				] [
					newgame [@@f]
				]
			]
		]
	]
]

savename = "savegame"
LoadSGame = [
	replaceui main title [
		uitext "Main Menu - Save Game" 1.5
	]
	replaceui [@arg1] content [
		uivlist 0.01 [
			UIVScroller .9 .725 0.025 .5 [
				uivlist 0.01 [
					uialign -1 -1
					loopfiles f (datapath "saves") sgz [
						UIBasicButton [
							uiclip .87 .1 [ uifill .87 .1 [
								uihlist .02 [
									uiimage (concatword (datapath "saves") / [@@@@@f] ".png") .1 .1 [
										uialign -1 0
										uialtimage $logo
									]
									uitext [@@@@@f] 1.25 .65
								]
							]]
						] [
							savename = [@@f]
						]
					]
				]
			]
			uihlist 0 [
				uialign 1 0
				UIField savename -35 "" 1 $UISafeFilter
				uioffset 0.05 0
				UIButton "Save" [
					lasthud = $hidehud
					hidehud 1
					sleep 0 [
						savegame $savename
						hidehud @lasthud
					]
					hideui [@@@@arg1]
				] [uialign 0 -1]
			]
		]
	]
]

LoadLGame = [
	replaceui main title [
		uitext "Main Menu - Load Game" 1.5
	]
	replaceui [@arg1] content [
		UIVScroller .9 .8 0.025 .5 [
			uivlist 0.01 [
				uialign -1 -1

				if $mainmenu [
					loopfiles d games cfg [
						uitext $d 1.5
						loopfiles f [games/@[d]/saves/] sgz [
							local name
							name = [@[d]/saves/@[f]]
							UIBasicButton [
								uiclip .87 .1 [ uifill .87 .1 [
									uihlist .02 [
										uiimage [games/@[name].png] .1 .1 [
											uialign -1 0
											uialtimage $logo
										]
										uitext [@@@@@f] 1.25 .65
									]
								]]
							] [
								loadgame @(escape $name)
							]
						]

						uifill 0 0.02
					]
				] [
					loopfiles f (datapath "saves") sgz [
						UIBasicButton [
							uiclip .87 .1 [ uifill .87 .1 [
								uihlist .02 [
									uiimage (concatword (datapath "saves") / [@@@@@f] ".png") .1 .1 [
										uialign -1 0
										uialtimage $logo
									]
									uitext [@@@@@f] 1.25 .65
								]
							]]
						] [
							loadgame @(escape $f)
						]
					]
				]
			]
		]
	]
]

LoadGameOptions = [
	replaceui [@arg1] options [
		uialign 0 0
		uivlist 0.01 [
			uialign -1 -1
			uitext "Gameplay" 1.25
			uioffset .03 0 [
				uitable 2 .01 [
					uitext "Ranged Attacks" 1
					uihlist 0.01 [
						UIRadioButton "Preserve Momentum" firemethod 0
						UIRadioButton "Redirect" firemethod 1
					]
				]
			]
			uitext "AI" 1.25
			uioffset .03 0 [
				uitable 2 .01 [
					uitext "AI Responsiveness" 1
					uihlist 0.01 [
						UIRadioButton "V. Low" r_aiperiod 1000
						UIRadioButton "Low" r_aiperiod 500
						UIRadioButton "Medium" r_aiperiod 100
						UIRadioButton "High" r_aiperiod 0
					]

					uitext "Waypoint draw distance" 1
					uihlist 0.01 [
						UIRadioButton "V. Low" waypointdrawdist 128
						UIRadioButton "Low" waypointdrawdist 256
						UIRadioButton "Medium" waypointdrawdist 512
						UIRadioButton "High" waypointdrawdist 1024
						UIRadioButton "V. High" waypointdrawdist 1536
					]
				]
			]
			uitext "Minimap" 1.25
			uioffset .03 0 [
				uitable 2 .01 [
					UICheckBox "Enabled" minimap 1 0
					uifill 0 0
					uitext "Orientation" 1
					uihlist 0.01 [
						UIRadioButton "Forward" minimapup 1 [minimap 1]
						UIRadioButton "North" minimapup 0 [minimap 1]
					]
					uitext "Maximum coverage" 1
					uihlist 0.01 [
						UIRadioButton "25%" minimapfrac .25 [minimap 1]
						UIRadioButton "50%" minimapfrac .5 [minimap 1]
						UIRadioButton "100%" minimapfrac 1 [minimap 1]
					]
					uitext "Maximum distance" 1
					uihlist 0.01 [
						UIRadioButton "V. Low" minimapmaxdist 512 [minimap 1]
						UIRadioButton "Low" minimapmaxdist 1024 [minimap 1]
						UIRadioButton "Medium" minimapmaxdist 1536 [minimap 1]
						UIRadioButton "High" minimapmaxdist 2560 [minimap 1]
						UIRadioButton "V. High" minimapmaxdist 4096 [minimap 1]
					]
				]
			]
			uitext "Projectiles" 1.25
			uioffset .03 0 [
				uitable 2 .01 [
					UICheckBox "Allow Flares" projallowflare 1 0
					UICheckBox "Allow Lights" projallowlight 1 0
					uitext "Maximum Ricochets" 1
					uihlist 0.01 [
						UIRadioButton "V. Low" maxricochets 1
						UIRadioButton "Low" maxricochets 5
						UIRadioButton "Medium" maxricochets 20
						UIRadioButton "High" maxricochets 50
						UIRadioButton "V. High" maxricochets 100
					]
					uitext "Particle Multiplier" 1
					uihlist 0.01 [
						UIRadioButton "V. Low" partmul 0.1
						UIRadioButton "Low" partmul 1
						UIRadioButton "Medium" partmul 2
						UIRadioButton "High" partmul 5
						UIRadioButton "V. High" partmul 10
					]
					uitext "Max Trail Steps" 1
					uihlist 0.01 [
						UIRadioButton "V. Low" linemaxsteps 8
						UIRadioButton "Low" linemaxsteps 32
						UIRadioButton "Medium" linemaxsteps 128
						UIRadioButton "High" linemaxsteps 256
						UIRadioButton "V. High" linemaxsteps 1024
					]
					uitext "Minimum Step Size" 1
					uihlist 0.01 [
						UIRadioButton "Large" linemininterval 32
						UIRadioButton "Average" linemininterval 8
						UIRadioButton "Small" linemininterval 1
					]
				]
			]
			uitext "Editmode" 1.25
			uioffset 0.03 0 [
				UICheckBox "Reset game state" edittogglereset 1 0
			]
			uioffset 0.03 0 [
				UICheckBox "Render entities" editdrawgame 1 0
			]
			uitext "Debug" 1.25
			uioffset .03 0 [
				uitable 4 .01 [
					UIBitField "World" debug (<< 1 0)
					UIBitField "Entity" debug (<< 1 1)
					UIBitField "Configuration" debug (<< 1 2)
					UIBitField "Projectiles" debug (<< 1 3)
					UIBitField "Script" debug (<< 1 4)
					UIBitField "AI" debug (<< 1 5)
					UIBitField "I/O" debug (<< 1 6)
					UIBitField "Cutscenes" debug (<< 1 7)
					UIBitField "Status Effects" debug (<< 1 8)
					UIBitField "Verbose" debug (<< 1 9)
				]
			]
		]
	]
]

UIMenuNoTitle "chat" [
	uivlist 0.05 [
		uialign -1 -1
		uihlist 0.02 [
			uiclip .3 .4 [
				uivlist 0 [
					local portraitcmd name

					case (r_reftype talker) $REF_CHAR [
						r_select_char talker [
							name = $r_char_name
							local suffix

							r_relationcondent player talker [
								suffix = "angry"
							] [
								suffix = "suspicious"
							] [
								suffix = "neutral"
							] [
								suffix = "happy"
							]

							portraitcmd = [
								uiimage @(escape (concatword (r_iconpath portraits/) $r_char_portrait _ $suffix)) .3 .3 [
									uialtimage (r_iconpath [portraits/default_@@@[suffix]])
								]
							]
						]
					] $REF_CONTAINER [
						r_select_container talker [
							name = $r_container_name
							portraitcmd = [
								uimodelpreview @(escape $r_container_mdl) @(findanims mapmodel) [] .3 .3
							]
						]
					] $REF_TRIGGER [
						r_select_trigger talker [
							name = $r_trigger_name
							portraitcmd = [
								@(if (& $r_trigger_flags $TRIG_INVIS) [
									result [
										uiimage media/interface/lamiae .3 .3
									]
								] [
									result [
										uimodelpreview @(escape $r_trigger_mdl) @(findanims mapmodel) [] .3 .3
									]
								])
							]
						]
					] $REF_ITEM [
						r_select_item talker [
							name = $r_item_name
							portraitcmd = [
								uimodelpreview @(escape $r_item_mdl) @(findanims mapmodel) () .3 .3
							]
						]
					]

					uicolor 0x5FCF9F5F .3 .3 [
// 						echo portraitcmd = $portraitcmd
						portraitcmd
					]
					uitext $name 1 0 [ uialign 0 0 ]
				]
			]

			UIVScroller .68 .4 .03 1 [
				uitext (r_get_dialogue) 1.25 .65
			]
		]
		UIVScroller 1 .4 0.03 1 [
			uitable 2 0.01 [
				uialign -1 -1
				loop i (r_num_response) [
					uiclip 0.04 0.03125 [
						uitext (concatword (+ @i 1) ") ") 1.25
					]
					UIBasicButton [
						uifill 0.92 0 [
							uitext (r_get_response @i) 1.25 .92
						]
					] [
						r_trigger_response @i
					] [ uiclamp 1 1 1 1]
				]
			]
		]
	]
]

refreshchat = [showchat]

refreshtrade = [
	GenTrade [trade]
]

UIMenu "trade" [
	uivlist .025 [
		uihlist 0 [
			UIVScroller .32 .8 .02 1 [
				uitag sell []
			]
			uivlist 0 [
				uifill .42 .2 [
					uispace 0.02 0 [
						uitag summary []
					]
				]
				UIVScroller .42 .6 .02 1 [
					uihlist 0 [
						uitag sellstack []
						uispace 0.004 0 [
							uiclamp 0 0 1 1
							uicolor 0xFFCFCFCF 0.002 0 [ uiclamp 0 0 1 1 ]
						]
						uitag buystack []
					]
				]
			]
			UIVScroller .32 .8 .02 1 [
				uitag buy []
			]
		]

		uihlist 0.05 [
			uialign 1 1
			UIButton "Trade" [ if (r_dotrade) [refreshtrade] [ hudline "Make me a better offer." ] ]
			UIButton "Cancel" [ hideui trade ]
		]
	]
] "Barter"

filteritem = [
	local res
	res = 1;
	r_select_item $arg1 [
		if $r_item_quantity [] [
			res = 0;
		]
		if (& $r_item_flags $arg2) [
			res = 0;
		]
	]

	result $res
]

UIItemTooltip = [
	UITooltip [
		uivlist 0.01 [
			uialign -1 -1
			uihlist 0.01 [
				uialign -1 0
				uiimage (r_iconpath $r_item_icon) .1 .1 [

					uialtimage (r_iconpath "default")
				]
				uivlist 0.025 [
					uitext (concat $r_item_quantity x $r_item_name) 1 0
					uihlist 0.01 [
						uialign 1 0
						if (& $r_item_flags $ITEM_QUEST) [
							uiimage (r_iconpath "quest") .04 .04
						]
						if (& $r_item_flags $ITEM_CURSED) [
							uiimage (r_iconpath "cursed") .04 .04
						]
						if (& $r_item_flags $ITEM_NATURAL) [
							uiimage (r_iconpath "natural") .04 .04
						]
						if (& $r_item_flags $ITEM_STOLEN) [
							uiimage (r_iconpath "stolen") .04 .04
						]
					]
				]
			]
			uitext $r_item_description .8 .4
			uitable 2 0 [
				uiclamp
				uitext "Category	" 1
				uioffset .025 0 [
					uialign 1 0
					uitext (r_cat_get $r_item_category) 1 0 [uialign 1 0]
				]
				if $r_item_maxdurability [
					uitext "Durability	" 1
					uioffset .025 0 [
						uialign 1 0
						uitext (concat $r_item_durability / $r_item_maxdurability) 1 0 [uialign 1 0]
					]
				]
				if (>= $r_item_charges 0) [
					uitext "Charges	" 1
					uioffset 0.025 0 [
						uialign 1 0
						uitext (concat $r_item_charges left) 1
					]
				]
				uitext "Weight	" 1
				uioffset .025 0 [
					uitext (concatword $r_item_weight " (" (*f $r_item_quantity $r_item_weight) ")") 1 0 [uialign 1 0]
				]
				uitext "Value	" 1
				uioffset .025 0 [
					uialign 1 0
					uitext (concatword $r_item_value " (" (* $r_item_quantity $r_item_value) ")") 1 0 [uialign 1 0]
				]
			]
			uitable 2 0.005 [
				uialign -1 -1
				loop i (r_num_item_use) [
					r_select_static_item_use $i [
						uiimage (r_iconpath $r_item_use_icon) .05 .05 [
							uialtimage (r_iconpath $r_item_icon)
							uialtimage (r_iconpath "default")
						]
						uivlist 0 [
							uitext $r_item_use_name 1 .3495
							uitext $r_item_use_description .8 .3495
						]
					]
				]
			]
		]
	]
]

UIItemUseTooltip = [
	UITooltip [
		uivlist 0.01 [
			uialign -1 -1
			uihlist 0.01 [
				uialign -1 0
				uiimage (r_iconpath $r_item_use_icon) .1 .1 [
					uialtimage (r_iconpath $r_item_icon)
					uialtimage (r_iconpath "default")
				]
				uivlist 0.01 [
					uitext $r_item_use_name 1 0
					uitext $r_item_use_description .8 .27
					//TODO show chargeflags here?
// 					uihlist 0.01 [
// 						uialign 1 0
// 						if (& $r_item_flags $ITEM_QUEST) [
// 							uiimage (r_iconpath "quest") .04 .04
// 						]
// 						if (& $r_item_flags $ITEM_CURSED) [
// 							uiimage (r_iconpath "cursed") .04 .04
// 						]
// 						if (& $r_item_flags $ITEM_NATURAL) [
// 							uiimage (r_iconpath "natural") .04 .04
// 						]
// 						if (& $r_item_flags $ITEM_STOLEN) [
// 							uiimage (r_iconpath "stolen") .04 .04
// 						]
// 					]
				]
			]
			if (>= $r_item_use_type $USE_ARMOUR) [
				uitable 2 0 [
					uitext "Slot(s):	"
					if $r_item_use_slots [
						local slot
						slot = ""
						loop i (listlen $slots) [
							if (& $r_item_use_slots (<< 1 $i)) [
								slot = (concat $slot (escape (at $slots $i)))
							]
						]
						uitext (prettylist $slot) 1 0 [uialign 1 0]
					] [
						uitext "None" 1 0 [uialign 1 0]
					]
					if (>= $r_item_use_skill 0) [
						uitext "Skill:	"
						uitext (concatword  (at $skills $r_item_use_skill) " (" (r_get_skill player $r_item_use_skill) ")") 1 0 [uialign 1 0]
					]
					local stat skill
					stat = ""
					skill = ""
					loop i (listlen $lstats) [
						local val
						val = $[r_item_use_reqs_@(at $lstats $i)]
						if (> $val 0) [
							local colour
							if (>= (r_get_attr player $i) $val) [
								colour = 0x40FF40
							] [
								colour = 0xFF4040
							]
							stat = (concat $stat ";" uicolortext (escape [@(at $sstats $i) @val]) .8 0 $colour)
						]
					]
					loop i (listlen $lskills) [
						local val
						val = $[r_item_use_reqs_@(at $lskills $i)]
						if (> $val 0) [
							local color;
							if (>= (r_get_attr player $i) $val) [
								colour = 0x40FF40
							] [
								colour = 0xFF4040
							]
							skill = (concat $skill ";" uicolortext (escape [@(at $sskills $i) @val]) .8 0 $colour)
						]
					]

					if $stat [
						uitext "Stats:	"
						uitable 4 0.01 [
							uialign 1 0
							stat
						]
					]
					if $stat [
						uitext "Skills:	"
						uitable 4 0.01 [
							 uialign 1 0
							skill
						]
					]
					if (>= $r_item_use_type $USE_WEAPON) [
						uitext "Target:	"
						uitext (at $targets $r_item_use_target) 1 0 [uialign 1 0]

						if (|| $r_item_use_cost [= (indexof $resv_ammo $r_item_use_ammo) -1]) [
							uitext "Ammo:	"
							r_select_static_ammo $r_item_use_ammo [
								uitext $r_ammo_name 1 0 [uialign 1 0]
							]

							uitext "Cost:	"
							uitext $r_item_use_cost 1 0 [uialign 1 0]
						]

						if $r_item_use_charge [
							uitext "Charge Rate:	"
							uitext (divf $r_item_use_charge 1000) 1 0 [uialign 1 0]
						]

						cond [ <= $r_item_use_target $T_AREA ] [
							if (!= $r_item_use_target $T_SINGLE) [
								uitext "Radius:	"
								uitext $r_item_use_radius 1 0 [uialign 1 0]
							]
							if (& $r_item_use_pflags $P_DIST) [
								uitext "Range:	"
								uitext $r_item_use_range 1 0 [uialign 1 0]
							]
							if (& $r_item_use_pflags $P_TIME) [
								uitext "Lifetime:	"
								uitext (divf $r_item_use_lifetime 1000) 1 0 [uialign 1 0]
							]
							if (& $r_item_use_pflags $P_RICOCHET) [
								uitext "Elasticity:	"
								uitext $r_item_use_elasticity 1 0 [uialign 1 0]
							]

							uitext "Speed:	"
							uitext (*f $r_item_use_speed 100) 1 0 [uialign 1 0]

						] [ <= $r_item_use_target $T_SAREA ] [
							if (!= $r_item_use_target $T_SELF) [
								uitext "Radius:	"
								uitext $r_item_use_radius 1 0 [uialign 1 0]
							]
						] () [ //melee
							uitext "Range:	"
							uitext $r_item_use_range 1 0 [uialign 1 0]
							uitext "Angle:	"
							uitext $r_item_use_angle 1 0 [uialign 1 0]
						]

						if $r_item_use_kickback [
							uitext "Kickback:	"
							uitext $r_item_use_kickback 1 0 [uialign 1 0]
						]
						if $r_item_use_recoil [
							uitext "Recoil:	"
							uitext $r_item_use_recoil 1 0 [uialign 1 0]
						]
					]
				]
			]

			//TODO show status effects here
		]
	]
]

//arg2; 1 == sell; 0 == buy
novalueitem = [
// TODO list currency item
// 	local rate res
// 	res = 1
// 	r_select_item $arg1 [
// 		r_select_merchant trader [
// 			rate = (r_merchant_getrate $r_item_category)
// 		]
// 		cond [ if
// 		if (! $r_item_value) [ res = 0 ]
// 		if (=f (at $rate $arg1) 0) [ res = 0 ]
//
// 	]
// 	result $res
	result 1
]

GenTrade = [
	r_setref sell
	r_setref sellstack
	r_setref buy
	r_setref buystack

	r_sellstack_get sellstack
	r_buystack_get buystack

	//TODO check for stolen, cursed and other misc properties;
	//don't list things that shouldn't be listable and don't trade in things with no value to the merchant
	r_loop_inv inv player [
		loop i (r_ref_len inv) [
			ref = [inv:@i]
			if (&& [filteritem $ref $ITEM_NATURAL] [novalueitem $ref 1]) [
				r_ref_push sell $ref
			]
		]
	]
	r_loop_inv inv trader [
		loop i (r_ref_len inv) [
			ref = [inv:@i]
			if (&& [filteritem $ref $ITEM_NATURAL] [novalueitem $ref 0]) [
				r_ref_push buy $ref
			]
		]
	]
	local extrasell extrabuy sl bl

	extrasell = (sl = (r_ref_len sell)
		max (- 24 $sl) (? (mod $sl 3) (- 3 (mod $sl 3 )) 0))
	extrabuy = (bl = (r_ref_len buy)
		max (- 24 $bl) (? (mod $bl 3) (- 3 (mod $bl 3 )) 0))

	local extrasellstack extrabuystack

	extrasellstack = (max 12 (r_ref_len sellstack) (r_ref_len buystack))
	extrasellstack = (+ $extrasellstack (mod $extrasellstack 2))
	extrabuystack = (- $extrasellstack (r_ref_len buystack))
	extrasellstack = (- $extrasellstack (r_ref_len sellstack))

	local ref

	replaceui [@arg1] summary [
		uitable 2 0.01 [
			uialign 0 0
			r_select_merchant trader [
				r_stack [
					r_setref_inv money player $r_merchant_currency

					uitext "Currency: " 1 0 [uialign 1 0]
					r_select_item money:0 [
						uitext $r_item_name 1 0 [uialign -1 0]
					]

					uitext "Store Funds: " 1 0 [uialign 1 0]
					uitext (r_get_amount trader $r_merchant_currency)  1 0 [uialign -1 0]

					uitext "Store Credit: " 1 0 [uialign 1 0]
					uitext $r_merchant_credit  1 0 [uialign -1 0]

					uitext "Your Funds: " 1 0 [uialign 1 0]
					uitext (r_get_amount player $r_merchant_currency)  1 0 [uialign -1 0]


					sellvalue = (r_sellstack_value)
					buyvalue = (r_buystack_value)

					uitext "Transaction Value: " 1 0 [uialign 1 0]
					if (> $buyvalue (+ $sellvalue $r_merchant_credit)) [
						uicolortext (- $sellvalue $buyvalue) 1 0 0xBFBF00 [uialign -1 0]
					] [
						uicolortext (- $sellvalue $buyvalue) 1 0 0xBFFFBF [uialign -1 0]
					]
				]
			]
		]
	]

	replaceui [@arg1] sell [
		uitable 3 0.01 [
			loop i (r_ref_len sell) [
				ref = [sell:@i]
				r_select_item $ref [
					UIBasicButton [
						uiimage (r_iconpath "empty") .09 .09
						uiimage (r_iconpath $r_item_icon) .09 .09 [
							uialtimage (r_iconpath "default")
							uialtimage $logo
						]
						uiclip .09 .09 [
							uialign 0 1
							uicolortext (r_get_amount $ref) .9 0 0xFFCF9F
						]
					] [
						r_sellstack_add [@@ref] 1
						refreshtrade
					] [
						UIItemTooltip
					]
				]
			]
			loop i $extrasell [
				uiimage (r_iconpath "empty") .09 .09
			]
		]
	]
	replaceui [@arg1] sellstack [
		uitable 2 0.01 [
			loop i (r_ref_len sellstack) [
				ref = [sellstack:@i]
				r_select_item $ref [
					UIBasicButton [
						uiimage (r_iconpath "empty") .09 .09
						uiimage (r_iconpath $r_item_icon) .09 .09 [
							uialtimage (r_iconpath "default")
							uialtimage $logo
						]
						uiclip .09 .09 [
							uialign 0 1
							uicolortext (r_get_amount $ref) .9 0 0xFFCF9F
						]
					] [
						r_sellstack_remove [@@ref] 1
						refreshtrade
					] [
						UIItemTooltip
					]
				]
			]
			loop i $extrasellstack [
				uiimage (r_iconpath "empty") .09 .09
			]
		]
	]
	replaceui [@arg1] buystack [
		uitable 2 0.01 [
			loop i (r_ref_len buystack) [
				ref = [buystack:@i]
				r_select_item $ref [
					UIBasicButton [
						uiimage (r_iconpath "empty") .09 .09
						uiimage (r_iconpath $r_item_icon) .09 .09 [
							uialtimage (r_iconpath "default")
							uialtimage $logo
						]
						uiclip .09 .09 [
							uialign 0 1
							uicolortext (r_get_amount $ref) .9 0 0xFFCF9F
						]
					] [
						r_buystack_remove [@@ref] 1
						refreshtrade
					] [
						UIItemTooltip
					]
				]
			]
			loop i $extrabuystack [
				uiimage (r_iconpath "empty") .09 .09
			]
		]
	]
	replaceui [@arg1] buy [
		uitable 3 0.01 [
			loop i (r_ref_len buy) [
				ref = [buy:@i]
				r_select_item $ref [
					UIBasicButton [
						uiimage (r_iconpath "empty") .09 .09
						uiimage (r_iconpath $r_item_icon) .09 .09 [
							uialtimage (r_iconpath "default")
							uialtimage $logo
						]
						uiclip .09 .09 [
							uialign 0 1
							uicolortext (r_get_amount $ref) .9 0 0xFFCF9F
						]
					] [
						r_buystack_add [@@ref] 1
						refreshtrade
					] [
						UIItemTooltip
					]
				]
			]
			loop i $extrabuy [
				uiimage (r_iconpath "empty") .09 .09
			]
		]
	]
]

showloot = [
	show_loot
	GenLoot _loot
	replaceui _loot title [
		uitext (r_get_name looter) 1.5
	]
]

UIMenu "_loot" [
	uihlist 0.025 [
		UIVScroller .42 .8 .02 1 [
			uitag player []
		]
		uifill .25 0 [
			uiclamp 1 1 1 1
			uivlist 0.04 [
				UIButton "Take All" [
					r_transfer looter player loot 0xFFFFFF
					GenLoot _loot
				]
				UIButton "Cancel" [
					hideui _loot
				]
			]
		]
		UIVScroller .42 .8 .02 1 [
			uitag loot []
		]
	]
]

GenLoot = [
	r_setref inventory
	r_setref loot

	local ref extrainv extraloot il ll

	r_loop_inv inv player [
		loop i (r_ref_len inv) [
			ref = [inv:@i]
			if (filteritem $ref (| $ITEM_QUEST $ITEM_NATURAL)) [
				r_ref_push inventory $ref
			]
		]
	]
	r_loop_inv inv looter [
		loop i (r_ref_len inv) [
			ref = [inv:@i]
			if (filteritem $ref $ITEM_NATURAL) [
				r_ref_push loot $ref
			]
		]
	]

	extrainv = (il = (r_ref_len inventory)
		max (- 32 $il) (? (mod $il 4) (- 4 (mod $il 4 )) 0))
	extraloot = (ll = (r_ref_len loot)
		max (- 32 $ll) (? (mod $ll 4) (- 4 (mod $ll 4 )) 0))

	replaceui [@arg1] player [
		uitable 4 .01 [
			loop i (r_ref_len inventory) [
				ref = [inventory:@i]
				r_select_item $ref [
					UIBasicButton [
						uiimage (r_iconpath "empty") .09 .09
						uiimage (r_iconpath $r_item_icon) .09 .09 [
							uialtimage (r_iconpath "default")
							uialtimage $logo
						]
						uiclip .09 .09 [
							uialign 0 1
							uicolortext (r_get_amount $ref) .9 0 0xFFCF9F
						]
					] [
						r_transfer player looter [@@ref] 1
						GenLoot [@@arg1]
					] [
						UIItemTooltip
					]
				]
			]
			loop i $extrainv [
				uiimage (r_iconpath "empty") .09 .09
			]
		]
	]
	replaceui [@arg1] loot [
		uitable 4 .01 [
			loop i (r_ref_len loot) [
				ref = [loot:@i]
				r_select_item $ref [
					UIBasicButton [
						uiimage (r_iconpath "empty") .09 .09
						uiimage (r_iconpath $r_item_icon) .09 .09 [
							uialtimage (r_iconpath "default")
							uialtimage $logo
						]
						uiclip .09 .09 [
							uialign 0 1
							uicolortext (r_get_amount $ref) .9 0 0xFFCF9F
						]
					] [
						r_transfer looter player [@@ref] 1
						GenLoot [@@arg1]
					] [
						UIItemTooltip
					]
				]
			]
			loop i $extraloot [
				uiimage (r_iconpath "empty") .09 .09
			]
		]
	]
]

showplayergui = [
	if (|| $mainmenu (hideui "player")) [] [
		showplayer
		case $playermenu 0 [
			LoadPGStats "player"
		] 1 [
			LoadPGInventory "player"
		] 2 [
			LoadPGEquipment "player"
		] 3 [
			LoadPGCrafting "player"
		] 4 [
			LoadPGEffects "player"
		] 5 [
			LoadPGJournal "player"
		] () [
			playermenu = 0
			LoadPGStats "player"
		]
	]
]

playermenu = 0
UIMenuNoClose "player" [
	uihlist 0.02 [
		uioffset 0.02 0 [
			uivlist 0.01 [
				UIBasicTab playermenu 0 [
					uiimage "media/interface/rpg_stats" .125 .125
				] [
					LoadPGStats "player"
				]
				UIBasicTab playermenu 1 [
					uiimage "media/interface/rpg_inv" .125 .125
				] [
					LoadPGInventory "player"
				]
				UIBasicTab playermenu 2 [
					uiimage "media/interface/rpg_equip" .125 .125
				] [
					LoadPGEquipment "player"
				]
				UIBasicTab playermenu 3 [
					uiimage "media/interface/rpg_craft" .125 .125
				] [
					LoadPGCrafting "player"
				]
				UIBasicTab playermenu 4 [
					uiimage "media/interface/rpg_effect" .125 .125
				] [
					LoadPGEffects "player"
				]
				UIBasicTab playermenu 5 [
					uiimage "media/interface/rpg_journal" .125 .125
				] [
					LoadPGJournal "player"
				]
			]
		]
		uifill 1.0 0.8 [
			uiclip 1.0 0.8 [
				uitag content []
			]
		]
	]
] "Player"

LoadPGStats = [
	replaceui [@arg1] content [
		r_select_char player [
			uialign -1 -1
			uihlist 0.04 [
				uitable 2 0 [
					uitext "Health: " 1
					uitext (concat (r_get_health player) / (r_get_maxhealth player)) 1
					uitext "H. Regen: " 1
					uitext (concat (r_get_healthregen player) / sec) 1
					uitext "Mana: " 1
					uitext (concat (r_get_mana player) / (r_get_maxmana player)) 1
					uitext "M. Regen: " 1
					uitext (concat (r_get_manaregen player) / sec) 1
					uitext "Weight: " 1
					uitext (concat (r_get_weight player) / (r_get_maxweight player)) 1
					uitext "Movespeed: " 1
					uitext (r_get_maxspeed player) 1
					uitext "Jump Vel: " 1
					uitext (r_get_jumpvel player) 1
					uifill 0 0.04
					uifill 0 0
					uitext "Level: " 1
					uitext (r_local_get player level) 1
					uitext "Experience: " 1
					uitext (r_local_get player experience) 1
					uitext "Needed EXP: " 1
					uitext (r_get_neededexp player) 1
					uitext "Stat Points: " 1
					uitext (r_local_get player statpoints) 1
					uitext "Skill Points: " 1
					uitext (r_local_get player skillpoints) 1
				]
				uitable 3 0 [
					uitext "Primary Stats" 1
					uifill 0 0; uifill 0 0
					loop i $STAT_MAX [
						uitext (concatword (at $stats $i) ": ") 1
						uitext (r_get_attr player $i) 1 0 [uialign 1 -1]
						uioffset 0.01 0 [
							if (r_local_get player statpoints) [

								UIBasicButton [
									uiimage "media/interface/invest" 0.025 0.025
								] [
									r_invest_attr player @i
									LoadPGStats [@@@@@@@@arg1]
								]
							] [
								uioffset 0.025 0.025
							]
						]
					]

					uioffset 0 0.02 [uitext "Skills" 1 0]
					uifill 0 0; uifill 0 0
					loop i $SKILL_MAX [
						uitext (concatword (at $skills $i) ":  ") 1
						uitext (r_get_skill player $i) 1 0 [uialign 1 -1]
						uioffset 0.01 0 [
							if (r_local_get player skillpoints) [
								UIBasicButton [
									uiimage "media/interface/invest" 0.025 0.025
								] [
									r_invest_skill player @i
									LoadPGStats [@@@@@@@@arg1]
								]
							] [
								uioffset 0.025 0.025
							]
						]
					]
				]
				uitable 3 0 [
					uitext "Type" 1
					uitext "Res." 1
					uioffset .01 0 [uitext "Th." 1 0]
					loop i $ATTACK_MAX [
						uitext (at $elements $i) 1 0 [uialign -1 0]
						uitext (r_get_resist player $i) 1 0 [uialign 0 0]
						uitext (r_get_thresh player $i) 1 0 [uialign 0 0]
					]
				]
			]
		]
	]
	replaceui [@arg1] title [
		uitext "Character Sheet" 1.5
	]
]

LoadPGInventory = [
	replaceui [@arg1] content [
		uihlist 0 [
			uifill 0.40 .8 [
				uitag examine [ ]
			]
			UIVScroller .6 .8 0.03 .2 [
				uitag inventory [ ]
			]
		]
	]
	GenInventory [@arg1]
	GenExamine [@arg1]

	replaceui [@arg1] title [
		uitext "Inventory" 1.5
	]
]

GenInventory = [
	local ref il
	r_setref inventory
	r_loop_inv stack player [
		loop i (r_ref_len stack) [
			ref = [stack:@i]
			r_select_item $ref [
				if $r_item_quantity [
					r_ref_push inventory $ref
				]
			]
		]
	]
	replaceui [@arg1] "inventory" [
		uitable 5 0.015 [
			uialign -1 -1
			loop i (r_ref_len inventory) [
				ref = [inventory:@i]
				r_select_item $ref [
					UIBasicButton [
						uiimage (r_iconpath "empty") .1 .1
						uiimage (r_iconpath $r_item_icon) .1 .1 [
							uialtimage (r_iconpath "default")
							uialtimage $logo
						]
						uiclip .1 .1 [
							uialign 0 1
							uicolortext (r_get_amount $ref) 1 0 0xFFCF9F
						]
					] [
						r_setref examine [@@ref]
						GenExamine [@@@@@@arg1]
					] [
						UIItemTooltip
					]
				]
			]
			loop i (il = (r_ref_len inventory)
				max (- 35 $il) (? (mod $il 5) (- 5 (mod $il 5 )) 0)
			) [
				uiimage (r_iconpath "empty") .1 .1
			]
		]
	]
]

dropamount = 1
GenExamine = [
	replaceui [@arg1] "examine" [
		if (= (r_reftype examine) $REF_INV) [
			uivlist 0 [
				uialign -1 -1
				r_select_item examine [
					uibutton () [
						loop i 3 [
							uiclip 0.4 .1 [
								uihlist 0.01 [
									uialign -1 0
									uiimage (r_iconpath $r_item_icon) .1 .1 [
										uialtimage (r_iconpath "default")
									]
									uivlist 0.025 [
										uitext (concat $r_item_quantity x $r_item_name) 1 0
										uihlist 0.01 [
											uialign 1 0
											if (& $r_item_flags $ITEM_QUEST) [
												uiimage (r_iconpath "quest") .04 .04
											]
											if (& $r_item_flags $ITEM_CURSED) [
												uiimage (r_iconpath "cursed") .04 .04
											]
											if (& $r_item_flags $ITEM_NATURAL) [
												uiimage (r_iconpath "natural") .04 .04
											]
											if (& $r_item_flags $ITEM_STOLEN) [
												uiimage (r_iconpath "stolen") .04 .04
											]
										]
									]
								]
							]
						]
						UIItemTooltip
					]
					uihlist 0 [
						UIBasicButton [
							uicolortext "Drop: " 1 0 0xFFFF7F
						] [
							r_drop player examine $dropamount
							GenExamine [@@@@@@arg1]
							GenInventory [@@@@@@arg1]
						] [uialign -1 0]
						UIField dropamount 6 "" 1 $UINumberFilter
					]
					uifill 0 0.025
					UIVScroller .4 .65 .02 0.5 [
						uivlist 0.01 [
							uiclamp 1 1 1 1
							if (! (r_num_item_use)) [
								uitext "This item cannot be used." 1 0.38
							]
							loop i (r_num_item_use) [
								r_select_item_use $i [
									//TODO hotkey binding stuffies, nyah!
									UIBasicButton [
										uifill 0.38 .1
										uihlist 0.01 [
											uiimage (r_iconpath $r_item_use_icon) 0.1 0.1 [
												uialtimage (r_iconpath $r_item_icon)
												uialtimage (r_iconpath "default")
											]
											uivlist 0.01 [
												uitext $r_item_use_name
												uitext $r_item_use_description .8 .27
											]
										]
									] [
										if (= @r_item_use_type $USE_CONSUME) [
											r_use player examine @@i
										] [
											r_equip player examine @@i
										]
										GenInventory [@@@@@@@@arg1]
										GenExamine [@@@@@@@@arg1]
									] [
										if (if (= $r_item_use_type $USE_CONSUME) [!= $r_item_charges 0] [ r_canequip player examine @@i ]) [] [
											uicolor 0x7FFF4040 0 0 [
												uiclamp 1 1 1 1
											]
										]
										UIItemUseTooltip
									]
								]
							]
						]
					]
				]
			]
		] [
			uifill 0.4 0 [
				uitext "No item selected - Select one to see extended detail and information." 1 0.4 [uialign -1 -1]
			]
		]
	]
]

selectedslot = -1
LoadPGEquipment = [
	replaceui [@arg1] content [
		r_stack [
			r_local equip
			uihlist 0.05 [
				uialign -1 0
				uitag ragdoll []
				UIVScroller .55 .8 .02 .5 [
					uitag equipment []
				]
			]
		]
	]
	replaceui [@arg1] title [
		uitext "Equipment" 1.5
	]
	GenPaperDoll [@arg1]
	GenEquipList [@arg1]
]

// args menu, slot, slotname
UIPaperDollSlot = [
	r_setref_equip equip player $arg2
	UIBasicButton [
		uiimage (r_iconpath "empty") .1 .1
		if (r_ref_len equip) [
			r_select_item equip [
				r_select_item_use equip [
					uiimage (r_iconpath $r_item_use_icon) .1 .1 [
						uialtimage (r_iconpath $r_item_icon)
						uialtimage (r_iconpath "default")
					]
				]
			]
		]
	] [
		selectedslot = @arg2
		GenPaperDoll [@@arg1]
		GenEquipList [@@arg1]
	] [
		if (r_ref_len equip) [
			r_select_item equip [
				r_select_item_use equip [
					UIItemUseTooltip
				]
			]
		] [
			UITooltip [ uitext [@@@@arg3] ]
		]
	]
]

GenPaperDoll = [
	replaceui [@arg1] ragdoll [
		uivlist 0 [
			uitable 3 .01 [
				UIPaperDollSlot $arg1 $SLOT_QUIVER "Quiver"
				UIPaperDollSlot $arg1 $SLOT_HEAD "Head"
				uifill 0 0
				uifill 0 0
				UIPaperDollSlot $arg1 $SLOT_TORSO "Torso"
				UIPaperDollSlot $arg1 $SLOT_ARMS "Arms"
				UIPaperDollSlot $arg1 $SLOT_RHAND "Right Hand"
				UIPaperDollSlot $arg1 $SLOT_LEGS "Legs"
				UIPaperDollSlot $arg1 $SLOT_LHAND "Left Hand"
				uifill 0 0
				UIPaperDollSlot $arg1 $SLOT_FEET "Feet"
				uifill 0 0
			]
			case $selectedslot -1 [
				uitext "Selected Slot: None" 1 0 [uialign 0 0]
			] 0 [
				uitext "Selected Slot: Misc." 1 0 [uialign 0 0]
			] () [
				local slot
				slot = ""
				loop i (listlen $slots) [
					if (& $selectedslot (<< 1 $i)) [
						slot = (concat $slot (escape (at $slots $i)))
					]
				]
				uitext (concat "Selected Slot:" (prettylist $slot)) 1 0 [uialign 0 0]
			]
			uioffset 0 .025 [
				uialign 0 0
				uitext "Miscellaneous."
			]
			UIVScroller .4 .275 .02 .5 [
				uialign -1 -1
				uitable 3 .025 [
					UIBasicButton [
						uiimage (r_iconpath "empty") .1 .1
					] [
						selectedslot = 0
						GenEquipList [@@@@@@arg1]
						GenPaperDoll [@@@@@@arg1]
					] [
						UITooltip [ uitext "Miscellaneous" ]
					]
					r_setref_equip equip player 0
					loop i (r_ref_len equip) [
						UIBasicButton [
							uiimage (r_iconpath "empty") .1 .1
							r_select_item equip [
								r_select_item_use equip [
									uiimage (r_iconpath $r_item_use_icon) .1 .1 [
										uialtimage (r_iconpath $r_item_icon)
										uialtimage (r_iconpath "default")
									]
								]
							]
						] [
							r_stack [
								r_local it
								r_setref_equip it player 0
								r_dequip player it:@@i
								GenPaperDoll [@@@@@@@@arg1]
								GenEquipList [@@@@@@@@arg1]
							]
						] [
							if (r_ref_len equip) [
								r_select_item equip [
									r_select_item_use equip [
										UIItemUseTooltip
									]
								]
							] [
								UITooltip [ uitext [@@@@arg2] ]
							]
						]
					]
				]
			]
		]
	]
]

GenEquipList = [
	replaceui [@arg1] equipment [
		uialign -1 -1
		if (>= $selectedslot 0) [
			uivlist 0.01 [
				r_stack [
					r_local equip
					r_setref_equip equip player $selectedslot

					if (r_ref_len equip) [
						uioffset 0.02 0 [
							UIBasicButton [
								uifill .51 .075 [
									uihlist 0.02 [
										uiclamp 1 1 1 1
										uiimage (r_iconpath "empty") .075 .075
										r_select_item equip [
											uitext (concat "Unequip" $r_item_name) 1 .415
										]
									]
								]
							] [
								r_dequip player -1 $selectedslot
								GenPaperDoll [@@@@@@@@arg1]
								GenEquipList [@@@@@@@@arg1]
							] [
								r_select_item equip [
									r_select_item_use equip [
										UIItemUseTooltip
									]
								]
							]
						]
					]
				]

				r_setref candidates
				r_loop_inv inv player [
					loop i (r_ref_len inv) [
						do [
							local add
							add = 0

							r_select_item inv:@i [
								loop i (r_num_item_use) [
									r_select_item_use $i [
										if (&& $r_item_quantity [>= $r_item_use_type $USE_ARMOUR] [|| [&& (! $r_item_use_slots) (! $selectedslot)] [& $r_item_use_slots $selectedslot]]) [
											add = 1;
										]
									]
								]
							]

							if $add [
								r_ref_push candidates inv:@i
							]
						]
					]
				]

				loop i (r_ref_len candidates) [
					uivlist 0 [
						r_select_item candidates:@i [
							uibutton [] [
								loop i 3 [
									uifill .51 .025 [
										uihlist 0.02 [
											uialign -1 0
											uiimage (r_iconpath "empty") .025 .025 [
												uiimage (r_iconpath $r_item_icon) .025 .025 [
													uialtimage (r_iconpath "default")
												]
											]
											uitext (concat $r_item_quantity x $r_item_name) 1 .485
										]
									]
								]
								UIItemTooltip
							]

							loop j (r_num_item_use) [
								r_select_static_item_use $j [
									if (|| [&& (! $r_item_use_slots) (! $selectedslot)] [& $r_item_use_slots $selectedslot]) [
										uioffset .02 0 [
											UIBasicButton [
												uifill .51 .075 [
													uihlist 0.02 [
														uialign -1 0
														uiimage (r_iconpath "empty") .075 .075 [
															uiimage (r_iconpath $r_item_use_icon) .075 .075 [
																uialtimage (r_iconpath $r_item_icon)
																uialtimage (r_iconpath "default")
															]
														]
														uivlist 0 [
															uitext $r_item_use_name
															uitext $r_item_use_description .8 .415
														]
													]
												]
											] [
												r_equip player candidates:@@i @@j
												GenPaperDoll [@@@@@@@@@@@@arg1]
												GenEquipList [@@@@@@@@@@@@arg1]
											] [
												if (r_canequip player candidates:@@i @@j) [] [
													uicolor 0x7FFF4040 0 0 [
														uiclamp 1 1 1 1
													]
												]
												UIItemUseTooltip
											]
										]
									]
								]
							]
						]
					]
				]
			]
		] [
			uialign 0 0
			uitext "No slot selected." 1 .53
		]
	]
]

selectedrecipe = ""
craftamount = 1
LoadPGCrafting = [
	replaceui [@arg1] content [
		uihlist 0.02 [
			uivlist 0 [
				UIVScroller 0.49 0.65 0.03 0.05 [ uitag recipes [] ]
				uifill 0.49 .15 [ uivlist 0.03 [
					uihlist 0 [
						uitext "Copies:"
						UIField craftamount 8 [ GenRecipes [@@@@@@arg1] ] 1 $UINumberFilter
					]
					UIBasicButton [
						uitext "CRAFT" 1.5 0
					] [
						r_use_recipe $selectedrecipe $craftamount
						GenRecipes [@@@@@@arg1]
					]
				]]
			]
			uivlist 0 [
				uitext "Ingredients" 1.25 0 [uialign -1 0]
				UIVScroller 0.49 0.235 0.02 0.05 [ uitag ingredients [] ]
				uitext "Catalysts" 1.25 0 [uialign -1 0]
				UIVScroller 0.49 0.235 0.02 0.05 [ uitag catalysts [] ]
				uitext "Products" 1.25 0 [uialign -1 0]
				UIVScroller 0.49 0.235 0.02 0.05 [ uitag products [] ]
			]
		]
	]
	GenRecipes [@arg1]
	replaceui [@arg1] title [
		uitext "Crafting" 1.5
	]
]

GenRecipes = [
	replaceui [@arg1] recipes [
		uialign -1 -1
		uivlist 0 [
			r_loop_recipe i [
				r_select_static_recipe $i [
					if (& $r_recipe_flags $RECIPE_KNOWN) [
						UIBasicButton [
							if (r_check_recipe $i) [
								uicolortext $r_recipe_name 1.25 0 0x7FFF7F
							] [
								uicolortext $r_recipe_name 1.25 0 0xFF4040
							]
						] [
							selectedrecipe = @@(escape $i)
							GenRecipes [@@@@@@@arg1]
						]
					]
				]
			]
		]
	]

	if (r_exists_recipe $selectedrecipe) [
		replaceui [@@arg1] ingredients [
			uialign -1 -1
			uivlist 0 [
				r_select_static_recipe $selectedrecipe [
					loop i (r_recipe_num_ingredient) [
						item = (r_recipe_get_ingredient $i)
						r_setref_inv item player (at $item 0)

						r_select_item item [
							str = (concatword (tabify (* (at $item 1) $craftamount) 2) (tabify (concatword "(" (r_get_amount player (at $item 0)) ") ") 2) $r_item_name)

							uibutton [] [
								UIItemTooltip
								loop j 3 [
									if (>= (r_get_amount player (at $item 0)) (* (at $item 1) $craftamount)) [
										uicolortext $str 1 0 0x7FFF7F
									] [
										uicolortext $str 1 0 0xFF4040
									]
								]
							]
						]
					]
				]
			]
		]

		replaceui [@@arg1] catalysts [
			uialign -1 -1
			uivlist 0 [
				r_select_static_recipe $selectedrecipe [
					loop i (r_recipe_num_catalyst) [
						item = (r_recipe_get_catalyst $i)
						r_setref_inv item player (at $item 0)
						r_select_item item [
							str = (concatword (tabify (at $item 1) 2) (tabify (concatword "(" (r_get_amount player (at $item 0)) ") ") 2) $r_item_name)

							uibutton [] [
								UIItemTooltip
								loop j 3 [
									if (>= (r_get_amount player (at $item 0)) (* (at $item 1) $craftamount)) [
										uicolortext $str 1 0 0x7FFF7F
									] [
										uicolortext $str 1 0 0xFF4040
									]
								]
							]
						]
					]
				]
			]
		]

		replaceui [@@arg1] products [
			uialign -1 -1
			uivlist 0 [
				r_select_static_recipe $selectedrecipe [
					loop i (r_recipe_num_product) [
						item = (r_recipe_get_product $i)
						r_setref_inv item player (at $item 0)
						r_select_item item [
							str = (concatword (tabify (* (at $item 1) $craftamount) 2) (tabify (concatword "(" (r_get_amount player (at $item 0)) ") ") 2) $r_item_name)

							uibutton [] [
								UIItemTooltip
								loop j 3 [
									uicolortext $str 1 .5 0xCFCF00
								]
							]
						]
					]
				]
			]
		]
	] [
		looplist v "ingredients catalysts products" [
			replaceui [@@@arg1] $v [
				uialign 0 0
				uitext "Select a recipe first"
			]
		]
	]
]

//args ref
UIDrawEffect = [
	uicolor 0xCF2F5F9F .32 .1 [
		uiclamp 1 1 1 1
		r_select_status $arg1 [
			uivlist 0 [
				uihlist 0.02 [
					uiimage (r_iconpath $r_status_icon) .075 .075
					uivlist 0 [
						uitext $r_status_name 1 .225
						uitext $r_status_description .8 .225
					]
				]

				loop i (r_get_status_num $arg1) [
					local st
					st = (r_get_status $arg1 $i)
					uibutton [] [
						loop i 3 [
							uihlist 0.02 [
								uiimage (r_iconpath (at $statusicons (at $st 0))) .05 .05 [
									uialtimage (r_iconpath "default")
								]

								uivlist 0 [
									case (at $st 2) -1 [
										uitext (concat "Strength:" (at $st 1))
										uitext "Duration: Infinite"
									] 0 [
										uitext (concat "Strength:" (at $st 1)) 1 .25
									] () [
										uitext (concat "Str:" (at $st 1))
										uitext (concat "Remain:" (divf (at $st 3) 1000))
									]
								]
							]
						]
						UITooltip [
							uivlist 0 [
								case (at $st 2) -1 [
									uitext (concat "Str:" (at $st 1))
									uitext "Duration: Infinite"
								] 0 [
									uitext (concat "Str:" (at $st 1)) 1 .25
								] () [
									uitext (concat "Str:" (at $st 1))
									uitext (concat "Remain:" (divf (at $st 3) 1000))
								]
								uitext (at $statusdecs (at $st 0)) .8 .4
							]
						]
					]
				]
			]
		]
	]
]

LoadPGEffects = [
	replaceui [@arg1] content [
		UIVScroller 1 .8 .02 .5 [
			uivlist 0 [
				uialign -1 -1
				uitext "Active Effects"
				uitable 3 .005 [
					r_loop_veffects eff player [
						UIDrawEffect eff
					]
				]
				uifill 0 .02
				uitext "Area Effects"
				uitable 3 .005 [
					r_loop_aeffects eff curmap player [
						UIDrawEffect eff
					]
				]
				uifill 0 .02
				uitext "Persistent Effects"
				uitable 3 .005 [
					uitext "TODO"
				]
			]
		]
	]
	replaceui [@arg1] title [
		uitext "Status Effects" 1.5
	]
]

bucket = ""
LoadPGJournal = [
	replaceui [@arg1] content [
		uihlist 0 [
			UIVScroller .4 .8 0.02 .4 [
				uivlist 0 [
					uialign -1 -1
					r_journal_loop_buckets b [
						UIBasicButton [
							local color blanket
							color = 0xFFFFFF
							blanket = 0

							case (r_journal_getstatus $b) $JRN_ACCEPTED [
								color = 0x2F5FFF
							] $JRN_RUMOUR [
								color = 0xFFFFFF
							] $JRN_MENTIONED [
								color = 0x9F9F9F
							] $JRN_COMPLETED [
								color = 0x5FFF5F
								blanket = 1
							] $JRN_BOTCHED [
								color = 0xFF2F2F
								blanket = 1
							]

							if $blanket [
								uitext $b 1.25
								uimodcolor $color 0 0 [ uiclamp 1 1 1 1 ]
							] [
								uicolortext $b 1.25 0 $color
							]
						] [
							bucket = [@@b]
							GenJEntries [@@@@@@arg1]
						]
					]
				]
			]
			UIVScroller .6 .8 0.02 .4 [
				uitag entries [uiclamp 1 1 1 1; uialign 0 -1]
			]
		]
	]
	replaceui [@arg1] title [
		uitext "Journal" 1.5
	]
	GenJEntries [@arg1]
]

GenJEntries = [
	replaceui [@arg1] entries [
		uivlist 0.01 [
			uiclamp 1 1 1 1
			uialign -1 -1
			r_journal_loop_entries e $bucket [
				uitext $e 1 .58
				uicolor 0x7FFFFFFF .2 .005 [ uialign 0 0 ]
			]
		]
	]
]

loopfiles f config/rpg/menus cfg [
    exec [config/rpg/menus/@[f].cfg]
]

GenQuickEntareatrigger = [
	uivlist 0.01 [
		uitable 2 0.01 [
			iterprops ["X-Rad" "Y-Rad" "Z-Rad"] [ 0 100 0 100 0 100 ] 0
			iterprops ["Period"] [ 1 120000 ] 4
		]

		uifill 0.02 0.02

		uitext "Flags" 1.25 0 [uialign 0 0]
		uiline 0xCFCFCF 0 0 [uiclamp 1 1 0 0]
		uihlist 0.01 [
			uialign 0 -1
			uivlist 0.01 [
				uitext "Signal Targets" 0.8

				UIBitField "Send to Map (default)" tmp3 $AT_SIGNALMAP entupdate
				UIBitField "Send to Critter" tmp3 $AT_SIGNALCRITTER entupdate
			]
			uivlist 0.01 [
				uitext "Period" 0.8

				UIBitField "On Entry (default)" tmp3 $AT_ONENTRY entupdate [
					UITooltip [ uitext "Triggers if an entity enters the specified domain, this test is performed every frame and can be slow as a record needs to be maintained" .9 .45 ]
				]
				UIBitField "On Exit (default)" tmp3 $AT_ONEXIT entupdate [
					UITooltip [ uitext "Triggers if an entity exits the specified domain, this test is performed every frame and can be slow as a record needs to be maintained" .9 .45 ]
				]
				UIBitField "On Period" tmp3 $AT_ONFIXEDPERIOD entupdate [
					UITooltip [ uitext "Tests after a period has elapsed unless ONFRAME is also set. This option is the most performant provided it is used alone." .9 .45 ]
				]
				UIBitField "On Frame" tmp3 $AT_ONFRAME entupdate
			]
			uivlist 0.01 [
				uitext "Test parameters" 0.8

				UIBitField "Player only (default)" tmp3 $AT_TESTPLAYER entupdate
				UIBitField "All Critters" tmp3 $AT_TESTCRITTERS entupdate [
					UITooltip [ uitext "Tests everything, including the player. This can get slow!" .9 .45 ]
				]
				UIBitField "External Area" tmp3 $AT_TESTEXTERNAL entupdate [
					UITooltip [ uitext "Flips the test so that outside entities set off the trigger" .9 .45 ]
				]
			]
		]
	]
]
