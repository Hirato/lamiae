r_script_signal "interact" [
	r_chat self (at "main brewing brewed" (r_local_get self brewing))
]

r_script_node "brewing" [ format "[The %1 inside is still brewing]" (r_local_get self brew) ] []

r_script_node "brewed" [ result "[The drink has fermented]" ] [
	r_response (format "[Collect the %1]" (r_local_get talker brew)) "" [
		r_additem player (r_local_get talker brew) (rnd 6 2)
		r_local_set talker "brewing" 0
	]
	r_response "[Leave it alone]" ""
]

r_script_node "main" [ result "[The keg stands before you, empty and ready to be used]" ] [
	local drinkcombos
	drinkcombos = [
		"grape juice" "wine"
		"apple juice" "apple cider"
		"strawberry juice" "strawberry cider"
	]

	//Player needs at least 1 unit of yeast
	if (r_get_amount player yeast) [
		looplist2 src dst $drinkcombos [
			if (>= (r_get_amount player $src) 5) [
				r_response (format "[Prepare a brew of %1]" $dst) "commenced" [
					r_remove_generic player @(escape $src) 5
					r_remove_generic player yeast 1

					r_local_set talker brewing 1
					r_local_set talker brew @(escape $dst)
					r_sleep 60000 [
						r_local_set talker brewing 2
					]
				]
			]
		]
	]
	r_response "[Leave it alone]" ""
]

r_script_node "commenced" [ result "[The process has begun and must be left undisturbed for some time]" ]
