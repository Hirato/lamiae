//logo <premul>media/interface/mylogohere

//bgflags
// 1 - caption
// 2 - mapshot/texture
// 4 - mapname
// 8 - mapinfo (usually a tip)


showbackground = [
	showui bg [
		uiclamp 1 1 1 1
		uialign 0 0
		uivgradient 0xFFEFCF 0xFFDFCF9F 0 0 [
			uiclamp 1 1 1 1
			if ( & $bgflags 6 ) [
				uioffset 0 .175 [
					uialign 0 -1
					uiimage $logo 0.3 0.3
				]
			] [
				uiimage $logo 0.8 0.8 [ uialign 0 0 ]
			]

			uispace .02 .02 [
				uialign -1 1
				uiimage "<premul>media/interface/cube2badge" .2 .1
			]

			if (& $bgflags 1) [
				uispace 0 .125 [
					uialign 0 1
					uitext $bgcaption 1.6
				]
			]

			if ( & $bgflags 6 ) [
				uioffset 0 .1 [
					uihlist .01 [
						uiimage $bgimage .35 .35 [
							uialtimage $logo
							uiimage "media/interface/mapshot_frame" .35 .35

							uitext $bgmapname (minf 1 (divf (*f $scr_w .32) (text_width $bgmapname))) 0 [ uialign 0 1 ]
						]

						if (& $bgflags 8) [
							uicolortext $bgmapinfo 1.2 .5 1 .85 .4
						]
					]
				]
			]

			if (>= $showversion (? $bgflags 2 1)) [
				uispace .05 .05 [
					uialign 1 1
					uitext $version 1.5
				]
			]
		]
	] [] 1
]
