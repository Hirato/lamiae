exec "data/stdui.cfg"

entcomplete = [ listcomplete $arg1 $enttypelist ]
entcomplete newent
entcomplete entfind
entcomplete entset
entcomplete enttype
entcomplete clearents
entcomplete =enttype

bind ESCAPE [
	do [
		@nokeybind [ if (hideui "main") [] [showmain] ]
	]
]

ColourField = [
	uivlist 0 [
		UIHSlider [@[arg1]1] 0 255 [ updateval @arg1 (mergecolour [@@arg1]) [@@arg2] ] 0.6 0.02 1 50 [ uimodcolor 1 .7 .7 0 0 [uiclamp 1 1 1 1] ]
		UIHSlider [@[arg1]2] 0 255 [ updateval @arg1 (mergecolour [@@arg1]) [@@arg2] ] 0.6 0.02 1 50 [ uimodcolor .6 1 .6 0 0 [uiclamp 1 1 1 1] ]
		UIHSlider [@[arg1]3] 0 255 [ updateval @arg1 (mergecolour [@@arg1]) [@@arg2] ] 0.6 0.02 1 50 [ uimodcolor .6 .8 1 0 0 [uiclamp 1 1 1 1] ]
	]
]

UIMenu "main" [
	uihlist 0.01 [
		uivlist 0.01 [
			UIButton "Load Map" [
				LoadMap main
			]
			UICondButton "Editing" [ $editing ] [
				LoadEditing main
			] [
				UITooltip [
					uicond [ $editing ] [
						uitext "Modify map properties and variables, set textures, spawn entities and other madness!" .9 .45
						uitext "You need to be in editmode to access this menu (F1)." .9 .45
					]
				]
			]
			UIButton "Multiplayer" [
				LoadMultiplayer main
			]
			UIButton "Options" [
				LoadOptions main
			]
			UIButton "Credits" [
				LoadCredits main
			]
			uispace 0 0.025
			UIButton "Exit" [ quit ]
		]
		uifill 0.9 0.8 [
			uiclip 0.9 0.8 [
				uitag content [
					uivlist 0 [
						uitext (concat "Welcome to Lamiae" $version) 2
					]
				]
			]
		]
	]
] "Main Menu - Welcome"

mapsmenu = 0
LoadMap = [
	replaceui $arg1 title [
		uitext "Main Menu - Load Map" 1.5
	]
	replaceui $arg1 content [
		uivlist 0.01 [
			uifill 0 0
			uihlist 0.01 [
				UITab mapsmenu 0 "Selections" [
					LoadMapSelections [@@@@@arg1]
				]
				UITab mapsmenu 1 "All" [
					LoadMapAll [@@@@@arg1]
				]
			]
			UIVScroller .9 .75 0.025 .5 [
				uitag maps []
			]
		]
	]
	mapsmenu = 0
	LoadMapSelections [@arg1]
]

LoadMapSelections = [
	replaceui [@arg1] maps [
		uitext "No selections are currently available" 1.5
	]
]

LoadMapAll = [
	replaceui [@arg1] maps [
		uivlist 0.005 [
			uialign -1 0
			uitext "packages/maps" 1.2 0
			uitable 6 .01 [
				loopfiles f "packages/maps" "ogz" [
					UIImageButton .135 .135 [
						map [@@f]
					] [
						uiimage [packages/maps/@f] .125 .125 [
							uialtimage "data/lamiae"
						]
					] $f
				]
			]
			loopdir d "packages/maps" [
				uioffset 0 .01
				uitext [packages/maps/@d] 1.2 0
				uitable 6 .01 [
					loopfiles f [packages/maps/@@d] "ogz" [
						UIImageButton .135 .135 [
							map [maps/@@@@[d]/@@[f]]
						] [
							uiimage [packages/maps/@@@@[d]/@@[f]] .125 .125 [
								uialtimage "data/lamiae"
							]
						] $f
					]
				]
			]
		]
	]
]

splitcolour = [
	[@[arg1]1] = (& (>> $arg2 16) 255)
	[@[arg1]2] = (& (>> $arg2 8) 255)
	[@[arg1]3] = (& (>> $arg2 0) 255)
]

mergecolour = [
	result (| (<< $[@[arg1]1] 16) (<< $[@[arg1]2] 8) (<< $[@[arg1]3] 0))
]

entedittype = 0
EntNormalField = [
	if (= $entedittype 1) [
		UIHSlider [@@arg1] [@@arg2] [@@arg3] [ entupdate ] 0.5 0.025 1 50
	] [
		UIField [@@arg1] 15 [
			@@arg1 = (clamp @@arg1 @@arg2 @@arg3)
			entupdate
		] 1 $UINumberFilter [ uialign -1 0 ]
	]
]

EntColourField = [
	if (= $entedittype 1) [
		uivlist 0 [
			UIHSlider [@@[arg1]1] 0 255 [ @arg1 = (mergecolour [@@arg1]); entupdate ] 0.5 0.02 1 50 [ uimodcolor 1 .7 .7 0 0 [uiclamp 1 1 1 1] ]
			UIHSlider [@@[arg1]2] 0 255 [ @arg1 = (mergecolour [@@arg1]); entupdate ] 0.5 0.02 1 50 [ uimodcolor .6 1 .6 0 0 [uiclamp 1 1 1 1] ]
			UIHSlider [@@[arg1]3] 0 255 [ @arg1 = (mergecolour [@@arg1]); entupdate ] 0.5 0.02 1 50 [ uimodcolor .6 .8 1 0 0 [uiclamp 1 1 1 1] ]
		]
	] [
		uihlist 0.01 [
			uialign -1 0
			UIField [@[arg1]1] 5 [
				[@@[arg1]1] = (clamp [@@[arg1]1] 0 255)
				@arg1 = (mergecolour [@@arg1]); entupdate
			] 1 $UINumberFilter
			UIField [@[arg1]2] 5 [
				[@@[arg1]2] = (clamp [@@[arg1]2] 0 255)
				@arg1 = (mergecolour [@@arg1]); entupdate
			] 1 $UINumberFilter
			UIField [@[arg1]3] 5 [
				[@@[arg1]3] = (clamp [@@[arg1]3] 0 255)
				@arg1 = (mergecolour [@@arg1]); entupdate
			] 1 $UINumberFilter
		]
	]
]

//props, limits, offset
iterprops = [
	loop i (listlen $arg1) [
		uitext (at $arg1 $i) 1 0 [ uialign -1 0 ]
		EntNormalField [tmp@(+ $i $arg3)] (at $arg2 (* $i 2)) (at $arg2 (+ (* $i 2) 1))
	]
]

newentgui = [
	alias [GenQuickEnt@arg1] [
		props = [@@arg2]
		limits = [@@arg3]

		if (strcmp $props "") [
			uitext "No attributes to configure"
		] [
			uitable 2 0.01 [
				newlims = ""
				looplist l $limits [
					newlims = (concat $newlims (l))
				]
				iterprops $props $newlims 0
			]
		]
	]
]

looplist ent $enttypelist [
	newentgui $ent [@(loopconcat i (getattrnum $ent) [escape (concat Attr $i)])] [
		@(loopconcat i (getattrnum $ent) [result [-10000 10000]])
	]
]

newentgui light [radius red green blue] [0 512 -256 256 -256 256 -256 256]
newentgui spotlight [radius] [0 200]
newentgui playerstart [yaw tag] [0 360 0 10000]
newentgui envmap [radius size blur] [0 400 0 9 0 2]
newentgui sound [type radius size] [0 20 0 500 0 500]

GenQuickEntparticles = [
	uihlist 0.05 [
		uivlist 0.01 [
			uialign -1 0
			cond [ && (<= $tmp0 2) (>= $tmp0 0) ] [
				uitext "Type" 1.25
				uitable 2 0.01  [
					UIRadioButton "Fire + Smoke" tmp0 0 [entupdate]
					UIRadioButton "Fire" tmp0 1 [entupdate]
					UIRadioButton "Smoke" tmp0 2 [entupdate]
				]
				uioffset 0 0.01
				uitable 2 0.01 [
					iterprops "Radius Height" [0 5000 0 5000] 1
					splitcolour tmp3 $tmp3
					uitext "Colour (RGB)" 1 0 [uialign -1 0]
					EntColourField tmp3
					iterprops "Size Speed Fade Gravity" [0 10000 -10000 10000 0 60000 -2000 2000] 4
				]
			] [ && (>= $tmp0 3) (<= $tmp0 4) ] [
				uitext "Type" 1.25
				uitable 2 0.01 [
					UIRadioButton "Smoke" tmp0 3 [entupdate]
					UIRadioButton "fountain" tmp0 4 [entupdate]
				]
				uioffset 0 0.01
				uitable 2 0.01 [
					iterprops "Direction Radius Fade Size Gravity" [ 0 5 0 10000 0 60000 0 10000 -2000 2000 ] 1
				]
			] [ = $tmp0 5 ] [
				uitable 2 0.01 [
					iterprops "Size" [-5 100] 1
					splitcolour tmp2 $tmp2
					uitext "Colour (RGB)" 1 0 [uialign -1 0]
					EntColourField tmp2
				]
			] [ && (>= $tmp0 6) (<= $tmp0 7) ] [
				uitext "Type" 1.25
				uitable 2 0.01 [
					UIRadioButton "Normal" tmp0 6 [entupdate]
					UIRadioButton "Versus" tmp0 7 [entupdate]
				]
				uioffset 0 0.01
				uitable 2 0.01 [
					iterprops "^"Captured Percentage^"" [0 100] 1
					splitcolour tmp2 $tmp2
					splitcolour tmp3 $tmp3
					uitext "Primary (RGB)" 1 0 [uialign -1 0]
					EntColourField tmp2
					uitext "Secondary (RGB)" 1 0 [uialign -1 0]
					EntColourField tmp3
				]
			] [ = $tmp0 8 ] [
				uitable 2 0.01 [
					iterprops "Tag" [ 0 10000 ] 1
					splitcolour tmp2 $tmp2
					uitext "Colour (RGB)" 1 0 [uialign -1 0]
					EntColourField tmp2
					iterprops "Size" [0 10000] 3
				]
			] [ && (>= $tmp0 9) (<= $tmp0 15) ] [
				uitext "Type" 1.25
				uitable 2 0.01 [
					UIRadioButton "Flare" tmp0 9 [entupdate]
					UIRadioButton "Lightning" tmp0 10 [entupdate]
					UIRadioButton "Fire" tmp0 11 [entupdate]
					UIRadioButton "Smoke" tmp0 12 [entupdate]
					UIRadioButton "Water" tmp0 13 [entupdate]
					UIRadioButton "Snow" tmp0 14 [entupdate]
					UIRadioButton "Leaves" tmp0 15 [entupdate]
				]
				uioffset 0 0.01
				uitable 2 0.01 [
					uitext "Style"
					UIHListSlider tmp1 [0 1 2 3 4 5 @(loopconcat i 64 [+ 256 $i])] [entupdate] .5 .025

					iterprops "Length" [0 10000] 2
					splitcolour tmp3 $tmp3
					uitext "Colour (RGB)" 1 0 [uialign -1 0]
					EntColourField tmp3
					iterprops "^"Fade Time^" Size Velocity Gravity" [-60000 60000 0 10000 -10000 10000 -2000 2000 ] 4
				]
			] [ && (>= $tmp0 32) (<= $tmp0 35) ] [
				uitext "Properties" 1.25
				uitable 2 0.01 [
					UIBitField "Fixed Size" tmp0 2 entupdate
					UIBitField "Sparkly" tmp0 1 entupdate
				]
				uioffset 0 0.01
				uitable 2 0.01 [
					iterprops "Red Green Blue Size" [0 255 0 255 0 255 0 10000] 1
				]
			]
		]
		uivlist 0.01 [
			uialign -1 0
			uitext "Change Type"
			local varieties
			varieties = [
				"Fire" "(rnd 3)"
				"Standard" "(rnd 5 3)"
				"Explosion" "5"
				"Bars" "(rnd 8 6)"
				"Text" "8"
				"Effect" "(rnd 16 9)"
				"Lens Flare" "(rnd 36 32)"
			]
			loop i (div (listlen $varieties) 2) [
				UISmallButton (at $varieties (* 2 $i)) [ tmp0 = @(at $varieties (+ (* 2 $i) 1)); entupdate; showquickgui ]
			]
		]
	]
]

GenQuickEntmapmodel = [
	uivlist 0.05 [
		uitable 2 0.01 [
			iterprops "Direction Model ^"Trigger Type^" ^"Trigger Tag^" Radius" [ 0 360 0 @(- (nummapmodels) 1) 0 29 0 200 0 1024]
			uitext "Colour (RGB)" 1 0 [uialign -1 0]
			splitcolour tmp5 $tmp5
			EntColourField tmp5
		]

		uitable	6 0.01 [
			UIBasicButton [
				uialign 0 0
				uicolor .5 .75 1 .2 .13 .13 [
					uiimage "data/lamiae" .12 .12
					uitext "-1" 1 0 [ uialign 0 1]
				]
			] [
				newent mapmodel -1
			] [
				UITooltip [
					uitext "(Invisible)" 1
				]
			]
			loop i (nummapmodels) [
				UIBasicButton [
					uialign 0 0
					uicolor .5 .75 1 .2 .13 .13 [
						uiimage [packages/models/@(mapmodelname $i)/shot] .12 .12 [
							uialtimage "data/lamiae"
						]
						uitext $i 1 0 [ uialign 0 1]
					]
				] [
					newent mapmodel @i
				] [
					UITooltip [
						uitext (mapmodelname $i) .9 .4
					]
				]
			]
		]
	]
]

editingmenu = 0
LoadEditing = [
	replaceui main title [
		uitext "Main Menu - Editing" 1.5
	]
	replaceui [@arg1] content [
		uivlist 0.01 [
			uifill 0 0
			uihlist 0.01 [
				UITab editingmenu 0 "Misc" [
					LoadMiscEdit [@@@@@arg1]
				]
				UITab editingmenu 1 "Entities" [
					LoadEntityEdit [@@@@@arg1]
				]
				UITab editingmenu 2 "Lighting" [
					LoadLightingEdit [@@@@@arg1]
				]
				UITab editingmenu 3 "Textures" [
					LoadTextureEdit [@@@@@arg1]
				]
				UITab editingmenu 4 "Skyboxes" [
					LoadSkyboxEdit [@@@@@arg1]
				]
				UITab editingmenu 5 "Brushes" [
					LoadBrushEdit [@@@@@arg1]
				]
			]
			UIVScroller .9 .75 0.025 .5 [
				uitag editopts []
			]
		]
	]
	editingmenu = 0
	LoadMiscEdit [@arg1]
]

showmats = 0
matfilter = 0
filtertype = 0
materials = "air water clip glass noclip lava aiclip respawn pain alpha"
matfilters = (concat "empty notempty solid notsolid" $materials)
newmapsize = 10
mapsavename = ""

listcomplete editmat $materials
looplist f $materials [
  alias $f [editmat @f $arg1]
]

LoadMiscEdit = [
	replaceui [@arg1] editopts [
		uialign -1 -1
		uivlist 0.01 [
			uitable 2 .01 [
				UISmallButton "Newmap" [ newmap $newmapsize ] [
					UITooltip [
						uivlist 0 [
							uitext "Creates a new, empty map..." .9
							uievaltext [format "Size: %1x%1x%1" (<< 1 $newmapsize)] .9
						]
					]
				]
				UIHSlider newmapsize 10 16 () .5 .03 1 500

				uifill 0 0; uifill 0 0
				UISmallButton "Save map as: " [
					savemap
				]
				mapsavename = (getcurmap)
				uitext (getcurmap) 1 0 [uialign -1 0]

				UISmallButton "Save map as: " [
					savemap $mapsavename
				]
				UIField mapsavename 20 [] 1 $UISafeFilter [uialign -1 0]
			]
			uifill 0 0.01

			UIExpandable "Materials" showmats [
				uihlist 0.01 [
					uivlist 0.01 [
						UICheckBox "Show Volumes" showmat 1 0
						looplist mat $materials [
							UISmallButton [@mat] [
								editmat @mat (? $matfilter (at $matfilters $matfilter) "")
							]
						]
					]
					uicolor 1 .5 .25 .2 0 0 [ uispace .01 0.01 [
						uivlist 0 [
							UICheckBox "Material Filter" matfilter 1 0 () [
								UITooltip [
									uitext "With this option on, material changes will only affect cubes which match the material filter.^neg. If you select Water below, material changes will only affect cubes with water in them.^nYou probably don't want this on." .9 .45
								]
							]
							looplist mat $matfilters [
								UIRadioButton $mat filtertype (indexof $matfilters $mat) [] [uialign -1 0 ]
							]
						]
					]]
				]
			]
		]
	]
]

lightprefabs = 0
particleprefabs = 0
mapmodellist = 0

LoadEntityEdit = [
	replaceui [@arg1] editopts [
		uialign -1 -1
		uiclamp 1 1 1 1
		uivlist 0.02 [
			uiclamp 1 1 1 1
			uitable 4 0.01 [
				uialign 0 0
				looplist v $enttypelist [
					UISmallButton (concat "New" $v) [newent @v]
				]
			]
			UIExpandable "Prefab. Lights" lightprefabs [
				uitable 6 0.01 [
					local varieties
					varieties = [
						"Blue" "128 0 0 255"
						"Green" "128 0 255 0
						"Orange" "128 192 128 0"
						"Pink" "128 255 160 160"
						"Purple" "128 255 0 255"
						"Turqoise" "128 0 255 255"
						"White" "128 255 255 255"
						"Yellow" "128 255 192 0"
					]
					loop i (div (listlen $varieties) 2) [
						UISmallButton (at $varieties (* 2 $i)) [ newent light @(at $varieties (+ (* 2 $i) 1)) ]
					]
				]
			]
			UIExpandable "Prefab. Particles" particleprefabs [
				uitable 6 0.01 [
					local varieties
					varieties = [
						"Rainfall" "13 300 100 0x2F7F4F -5000"
						"Snowfall" "14 300 100 0xFFFFFF -5000"
						"Torch" "0 70 50 0xAF5F2F"

					]
					loop i (div (listlen $varieties) 2) [
						UISmallButton (at $varieties (* 2 $i)) [ newent particles @(at $varieties (+ (* 2 $i) 1)) ]
					]
				]
			]
			UIExpandable "Mapmodels" mapmodellist [
				uitable	6 0.01 [
					UIBasicButton [
						uialign 0 0
						uicolor .5 .75 1 .2 .13 .13 [
							uiimage "data/lamiae" .12 .12
							uitext "-1" 1 0 [ uialign 0 1]
						]
					] [
						newent mapmodel -1
					] [
						UITooltip [
							uitext "(Invisible)" 1
						]
					]
					loop i (nummapmodels) [
						UIBasicButton [
							uialign 0 0
							uicolor .5 .75 1 .2 .13 .13 [
								uiimage [packages/models/@(mapmodelname $i)/shot] .12 .12 [
									uialtimage "data/lamiae"
								]
								uitext $i 1 0 [ uialign 0 1]
							]
						] [
							newent mapmodel @i
						] [
							UITooltip [
								uitext (mapmodelname $i) .9 .4
							]
						]
					]
				]
			]
		]
	]
]

LoadLightingEdit = [
	replaceui [@arg1] editopts [
		uialign -1 -1
		uitable 2 0.01 [
			uispace 0.01 0 [UISmallButton "Calculate Lights" [ calclight]]; uifill 0 0
			uitext "Ambience"
			splitcolour ambient $ambient
			ColourField ambient
			uitext "Sunlight"
			splitcolour sunlight $sunlight
			ColourField sunlight
			uitext "Sun Scale"
			UIHSlider sunlightscale 0 0 "" 0.6 0.025 0.1 100
			uitext "Sun Yaw"
			UIHSlider sunlightyaw 0 0 "" 0.6 0.025 0.5 50
			uitext "Sun Pitch"
			UIHSlider sunlightpitch 0 0 "" 0.6 0.025 0.5 50
		]
	]
]

LoadTextureEdit = [
	replaceui [@arg1] editopts [
		uialign -1 -1
		uitable 8 0.0075 [
			uialign -1 0
			loop i (texmrunum) [
				UIBasicButton [
					uicolor .2 .4 .6 .4 .1 .1 [uialign 0 0]
					uislotview @i .09 .09 [uialign 0 0]
					uitext @(texmruresolve $i) 0 0 [uialign 0 1] .9
				] [
					settex @(texmruresolve $i)
				] [
					UITooltip [
						uitext (gettexname (texmruresolve $i)) .9
					]
				]
			]
		]
	]
]

//dir, file exts, body
recursefiles = [
	looplist ext $arg2 [
		loopfiles file $arg1 $ext [
			file = [@[arg1]/@[file]]
			arg3
		]
	]
	loopdir d $arg1 [
		recursefiles [@[arg1]/@[d]] $arg2 $arg3
	]
]

showskyboxprops = 0
showcloudboxprops = 0
showcloudlayerprops = 0
showfogdomeprops = 0
skytype = 0

ConstructCloudLayers = [
	replaceui [@arg1] "skies" [
		uitable 6 0.01 [
			local cloud
			UIBasicButton [
				uicolor .5 .75 1 .2 .135 .135 [
					uialign 0 0
					uitext "None" 2
				]
			] [
				cloudlayer ""
			]
			recursefiles "packages/clouds" "png tga jpg" [
				cloud = (substr $file 9 (- (strlen $file) 9))
				//we use non-thumbnail versions of the skybox, since it's guaranteed to be loaded.
				UIBasicButton [
					uiimage [packages/@[skybox]_up] .135 .135
					if $cloudbox [
						uiimage [packages/@[cloudbox]_up] .135 .135
					]
					uiimage [<thumbnail>@file] .135 .135
				] [
					cloudlayer [@@cloud]
				]
			]
		]
	]
]

GenerateSkyList = [
	local skyboxes sky len
	skyboxes = ""
	len = (+ (strlen $arg1) 1)

	recursefiles $arg1 "png jpg tga" [
		if (>= (indexof "_dn _up _lf _ft _rt _bk" (substr $file (- (strlen $file) 3))) 0) [
			sky = (substr $file @@len (- (strlen $file) (+ @@len 3)))
			if (< (indexof $skyboxes $sky)) [
				skyboxes = (concat $skyboxes (escape $sky))
			]
		]
	]

	result $skyboxes
]

UISkyButton = [
	local sky prefix
	sky = $arg1
	prefix = $arg2

	UIBasicButton [
		uicolor .5 .75 1 0.2 0 0 [
			uialign 0 0
			uivlist 0.005 [
				uitable 4 0 [
					uiimage [<thumbnail>packages/@@@@[prefix]/@@@@[sky]_bk] 0.07 0.07
					uiimage [<thumbnail>packages/@@@@[prefix]/@@@@[sky]_rt] 0.07 0.07
					uiimage [<thumbnail>packages/@@@@[prefix]/@@@@[sky]_ft] 0.07 0.07
					uiimage [<thumbnail>packages/@@@@[prefix]/@@@@[sky]_lf] 0.07 0.07
				]
				uitext $sky 1
			]
		]
	]  [
		(? $skytype cloudbox skybox) [@@[prefix]/@@(unescape $sky)]
	] [
// 		UITooltip [
// 			uitable 4 0 [
// 				uifill 0 0
// 				uiimage (concatword "<thumbnail>packages/skyboxes/" $sky "_up") 0.1 0.1
// 				uifill 0 0
// 				uifill 0 0
// 				uiimage (concatword "<thumbnail>packages/skyboxes/" $sky "_bk") 0.1 0.1
// 				uiimage (concatword "<thumbnail>packages/skyboxes/" $sky "_rt") 0.1 0.1
// 				uiimage (concatword "<thumbnail>packages/skyboxes/" $sky "_ft") 0.1 0.1
// 				uiimage (concatword "<thumbnail>packages/skyboxes/" $sky "_lf") 0.1 0.1
// 				uifill 0 0
// 				uiimage (concatword "<thumbnail>packages/skyboxes/" $sky "_dn") 0.1 0.1
// 				uifill 0 0
// 				uifill 0 0
// 			]
// 		]
	]
]

ConstructSkyboxes = [
	replaceui [@arg1] "skies" [
		uitable 3 0.01 [
			local skyboxes

			if (= $skytype 1) [
				UIBasicButton [
					uicolor .5 .75 1 0.2 0.28 0.1 [
						uialign 0 0
						uitext "None" 2
					]
				] [
					cloudbox ""
				]

				skyboxes = (GenerateSkyList "packages/cloudboxes/")
				looplist sky $skyboxes [
					UISkyButton $sky "cloudboxes"
				]
			]
			skyboxes = (GenerateSkyList "packages/skyboxes/")
			looplist sky $skyboxes [
				UISkyButton $sky "skyboxes"
			]
		]
	]
]

LoadSkyboxEdit = [
	replaceui [@arg1] editopts [
		uialign -1 -1
		uivlist 0.005 [
			UIExpandable "Skybox Properties" showskyboxprops [
				uialign -1 0
				uitable 3 0.01 [
					uitext "Spin"
					UIHSlider spinsky 0 0 "" 0.6 0.025 0.1 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar spinsky ]
					uitext "Yaw"
					UIHSlider yawsky 0 0 "" 0.6 0.025 0.5 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar yawsky ]
					uitext "Colour (RGB)"
					splitcolour skyboxcolour $skyboxcolour
					ColourField skyboxcolour
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar skyboxcolour; splitcolour skyboxcolour $skyboxcolour ]
				]
			]
			UIExpandable "Cloudbox Properties" showcloudboxprops [
				uialign -1 0
				uitable 3 0.01 [
					uitext "Alpha"
					UIHSlider cloudboxalpha 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudboxalpha ]
					uitext "Clip"
					UIHSlider cloudclip 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudclip ]
					uitext "Spin"
					UIHSlider spinclouds 0 0 "" 0.6 0.025 0.1 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar spinclouds ]
					uitext "Yaw"
					UIHSlider yawclouds 0 0 "" 0.6 0.025 0.5 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar yawclouds ]
					uitext "Colour (RGB)"
					splitcolour cloudboxcolour $cloudboxcolour
					ColourField cloudboxcolour
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudboxcolour; splitcolour cloudboxcolour $cloudboxcolour ]
				]
			]
			UIExpandable "Cloudlayer Properties" showcloudlayerprops [
				uialign -1 0
				uitable 3 0.01 [
					uitext "Alpha"
					UIHSlider cloudalpha 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudalpha ]
					uitext "Fade"
					UIHSlider cloudfade 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudfade ]
					uitext "Height"
					UIHSlider cloudheight 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudheight ]
					uitext "Scale"
					UIHSlider cloudscale 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudscale ]
					uitext "Scroll-X"
					UIHSlider cloudscrollx 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudscrollx ]
					uitext "Scroll-Y"
					UIHSlider cloudscrolly 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudscrolly ]
					uitext "Spin"
					UIHSlider spincloudlayer 0 0 "" 0.6 0.025 0.1 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar spincloudlayer ]
					uitext "Yaw"
					UIHSlider yawcloudlayer 0 0 "" 0.6 0.025 0.5 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar yawcloudlayer ]
					uitext "Colour (RGB)"
					splitcolour cloudcolour $cloudcolour
					ColourField cloudcolour
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar cloudcolour; splitcolour cloudcolour $cloudcolour ]
				]
			]
			UIExpandable "Fogdome Properties" showfogdomeprops [
				uialign -1 0
				uitable 3 0.01 [
					uitext "Cap"
					UICheckBox "Draw" fogdomecap 1 0
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar fogdomecap ]
					uitext "Clip"
					UIHSlider fogdomeclip 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar fogdomeclip ]
					uitext "Height"
					UIHSlider fogdomeheight 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar fogdomeheight ]
					uitext "Min"
					UIHSlider fogdomemin 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar fogdomemin ]
					uitext "Max"
					UIHSlider fogdomemax 0 0 "" 0.6 0.025 0.01 50
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar fogdomemax ]
					uitext "Colour (RGB)"
					splitcolour fogdomecolour (? $fogdomecolour $fogdomecolour $fogcolour)
					ColourField fogdomecolour
					UIBasicButton [ uiimage "data/interface/revert" 0.025 0.025 ] [ resetvar fogdomecolour; splitcolour fogdomecolour $fogcolour ]
				]
			]

			uispace 0 0.01 [
				uialign 0 0
				uihlist 0.01 [

					uitext "Type: " 1 0
					UIRadioButton "Skybox" skytype 0 [ LoadSkyboxEdit [@@@@@arg1] ]
					UIRadioButton "Cloudbox" skytype 1 [ LoadSkyboxEdit [@@@@@arg1] ]
					UIRadioButton "Cloudlayer" skytype 2 [ LoadSkyboxEdit [@@@@@arg1] ]
				]
			]
			uitag skies [

			]
		]
	]
	if (= $skytype 2) [
		ConstructCloudLayers [@@arg1]
	] [
		ConstructSkyboxes [@@arg1]
	]
]

showskygui = [
	showmain
	LoadEditing main
	editingmenu = 4
	LoadSkyboxEdit main
]

showblendbrush = 0
showheightbrush = 0

LoadBrushEdit = [
	replaceui [@arg1] editopts [
		if (|| $hmapedit $blendpaintmode) [
			showblendbrush = (!= $blendpaintmode 0)
			showheightbrush = (!= $hmapedit 0)
		]
		uialign -1 -1

		uivlist 0.02 [
			UIExpandable "Blend Maps" showblendbrush [
				uivlist 0.01 [
					uioffset 0 0.01 [
						uitext "Paint Mode"
					]
					uitable 3 0.01 [
						UIRadioButton "off" blendpaintmode 0
						UIRadioButton "replace" blendpaintmode 1
						UIRadioButton "dig" blendpaintmode 2
						UIRadioButton "fill" blendpaintmode 3
						UIRadioButton "inverted dig" blendpaintmode 4
						UIRadioButton "inverted fill" blendpaintmode 5
					]

					uioffset 0 0.01 [
						uitext "Brushes"
					]
					uihlist 0.01 [
						UIBasicButton [uitext "Back"] [nextblendbrush -1]
						uifill 0.6 0 [
							uialign 0 0
							uicolorevaltext [ getblendbrushname (curblendbrush) ] 1 0 .4 1 .5
						]
						UIBasicButton [uitext "Next"] [nextblendbrush 1]
					]
					uitable 7 0.01 [
						loop i (numblendbrush) [
							UIBasicButton [
								uiimage [packages/brushes/@(getblendbrushname $i)] .1 .1
							] [
								setblendbrush (getblendbrushname @i)
							]
						]
					]
				]
			]
			UIExpandable "Heightfields" showheightbrush [
				uivlist 0.01 [
					uicolorevaltext [ concat "Current Brush:" $brushname ] 1 0 .4 1 .5
					uispace 0.1 0 [
						uitable 3 0.01 [
							UIBasicButton [
								uiimage "data/interface/brush_1c.png" .1 .1
							] [ brush_0]
							UIBasicButton [
								uiimage "data/interface/brush_21c.png" .1 .1
							] [ brush_1]
							UIBasicButton [
								uiimage "data/interface/brush_421c.png" .1 .1
							] [ brush_2]
							UIBasicButton [
								uiimage "data/interface/brush_3s.png" .1 .1
							] [ brush_3]
							UIBasicButton [
								uiimage "data/interface/brush_5s.png" .1 .1
							] [ brush_4]
							UIBasicButton [
								uiimage "data/interface/brush_7s.png" .1 .1
							] [ brush_5]
							UIBasicButton [
								uiimage "data/interface/brush_3s.png" .1 .1
							] [ brush_6]
							UIBasicButton [
								uiimage "data/interface/brush_5s.png" .1 .1
							] [ brush_7]
							UIBasicButton [
								uiimage "data/interface/brush_7s.png" .1 .1
							] [ brush_8]
						]
					]
				]
			]
		]
	]
]

showtexgui = [
	showmain
	LoadEditing main
	editingmenu = 3
	LoadTextureEdit main
]

entupdate = [
	do [ entset $tmpt @(loopconcat i (getattrnum $tmpt) [ result $[tmp@i] ]) ]
]

LoadQuickEdit = [
	cond [ || $blendpaintmode $hmapedit ] [
		editingmenu = 5
		LoadBrushEdit [@arg1]
	] [	enthavesel ] [
		editingmenu = -1
		replaceui [@arg1] editopts [
			uialign 0 0
			uivlist 0 [
				tmpt = (enttype)
				loop i (getattrnum (enttype)) [
					[tmp@i] = (entattr $i)
				]

				uitext (concat Entity: (enttype)) 1.5
				uihlist 0.02 [
					uitext "Input Type" 1
					UIRadioButton "Fields" entedittype 0 [ LoadQuickEdit [@@@@@arg1] ]
					UIRadioButton "Sliders" entedittype 1 [ LoadQuickEdit [@@@@@arg1] ]

				]
				uioffset 0 0.02
				[GenQuickEnt@(enttype)]
			]
		]
	] //else misc options
]

showquickgui = [
	showmain
	LoadEditing main
	LoadQuickEdit main
]

LoadMultiplayer = [
	replaceui [@arg1] title [
		uitext "Main Menu - Multiplayer" 1.5
	]
	replaceui [@arg1] content [
		uitext "What is mule-tea-play-argh?"
	]
]

optionsmenu = 0
LoadOptions = [
	replaceui main title [
		uitext "Main Menu - Options" 1.5
	]
	replaceui [@arg1] content [
		uivlist 0.01 [
			uifill 0 0
			uihlist 0.01 [
				UITab optionsmenu 0 "Misc" [
					LoadMiscOptions [@@@@@arg1]
				]
				UITab optionsmenu 1 "Audio" [
					LoadAudioOptions [@@@@@arg1]
				]
				UITab optionsmenu 2 "Video" [
					LoadVideoOptions [@@@@@arg1]
				]
				UITab optionsmenu 3 "Game" [
					LoadGameOptions [@@@@@arg1]
				]
				UITab optionsmenu 4 "Editmode" [
					LoadEditOptions [@@@@@arg1]
				]
				UITab optionsmenu 5 "Autoexec" [
					LoadAutoexecOptions [@@@@@arg1]
				]
			]
			uialign -1 -1
			UIVScroller .9 .75 0.025 .5 [
				uitag options []
			]
		]
	]
	optionsmenu = 0
	LoadMiscOptions [@arg1]
]

showcrosshairs = 0
crosshairs = "default"
crosshairtype = 0

showfonts = 0
showfullconfilter = 0
showconfilter = 0
showminiconfilter = 0
showhudopts = 0
filters = [
	"Important Information"
	Warnings
	Errors
	"Debug Messages"
	"Init Messages"
	"Script Messages"
]

LoadMiscOptions = [
	replaceui [@arg1] options [
		uialign  -1 -1
		uivlist 0.01 [
			UIExpandable "Crosshairs" showcrosshairs [
				uivlist 0.01 [
					uihlist 0.02 [
						uitext "Size:"
						UIHSlider crosshairsize 0 0 "" 0.6 0.025 1 100
					]
					UICheckBox "Colour Effects" crosshaircol 1 0
					UICheckBox "Image Effects" crosshairimg 1 0
					uioffset 0 0.01
					uitext "Crosshairs"
					uihlist 0.01 [
						uialign 0 0
						loop i (listlen $crosshairs) [
							UIRadioButton (at $crosshairs $i) crosshairtype $i
						]
					]

					uitable 15 0.005 [
						uialign 0 0
						loopfiles f packages/crosshairs png [
							f = [packages/crosshairs/@[f]]
							UIBasicButton [ uiimage $f 0.05 0.05 ] [
								loadcrosshair (result [@@f]) $crosshairtype
							]
						]
					]
				]
			]
			UIExpandable "Heads Up Display" showhudopts [
				uitable 2 0.01 [
					UICheckBox "Display HUD" hidehud 0 1
					uihlist 0.01 [
						UICheckBox "Display Stats" hidestats 0 1
					]
					UICheckBox "Show FPS" showfps 1 0
					uihlist 0.01 [
						UICheckBox "Show Range" showfpsrange 1 0
					]
					UICheckBox "Clock" wallclock 1 0
					uihlist 0.01 [
						UICheckBox "24-hour" wallclock24 1 0
						UICheckBox "Seconds" wallclocksecs 1 0
					]
					uioffset 0.04 0 [
						uitext "Edit Stats"
					]
					uihlist 0.01 [
						UIRadioButton "Editmode" showeditstats 0
						UIRadioButton "Always" showeditstats 1
					]
					UICheckBox "Damage Compass" damagecompass 1 0
					uifill 0 0
					UICheckBox "Damage Overlay" damagescreen 1 0
					uifill 0 0
					uioffset 0.04 0 [
						uitext "Minimap Detail"
					]
					uihlist 0.01 [
						UIRadioButton "Low" minimapsize 7
						UIRadioButton "Medium" minimapsize 8
						UIRadioButton "High" minimapsize 10
					]
				]
			]
			UIExpandable "Fonts" showfonts [
				uivlist 0.01 [
					uitext "If you wish to retain the font permanently write ^fs^f0setfont ^"^f5fontname^f0^"^fr within your autoexec.cfg" 1 0.75

					uitable 3 0.01 [
						uialign 0 0
						loopfiles f packages/fonts cfg [
							UIBasicButton [
								uivlist 0.01 [
									uiimage [packages/fonts/@[f]0.png] 0.22 0.22
									uitext [@f]
								]
							] [
								setfont [@@f]
							]
						]
					]
				]
			]
			UIExpandable "Full Console" showfullconfilter [
				uitable 2 0.01 [
					uitext "Size (% of screen)" 1
					UIHSlider fullconsize 0 0 [] 0.5 0.025 1 50

					loop i (listlen $filters) [
						UIBitField (at $filters $i) fullconfilter (<< 1 $i)
					]
				]
			]
			UIExpandable "Console" showconfilter [
				uitable 2 0.01 [
					uitext "Size (Lines)" 1
					UIHSlider consize 0 0 [] 0.5 0.025 1 100
					uitext "Fade (Seconds)" 1
					UIHSlider confade 0 0 [] 0.5 0.025 1 100
					uitext "Scale" 1
					UIHSlider conscale 0.20 1 [] 0.5 0.025 .01 50
					loop i (listlen $filters) [
						UIBitField (at $filters $i) confilter (<< 1 $i)
					]
				]
			]
			UIExpandable "Mini Console" showminiconfilter [
				uitable 2 0.01 [
					uitext "Size (Lines)" 1
					UIHSlider miniconsize 0 0 [] 0.5 0.025 1 100
					uitext "Fade (Seconds)" 1
					UIHSlider miniconfade 0 0 [] 0.5 0.025 1 100
					uitext "Width (chars)" 1
					UIHSlider miniconwidth 0 0 [] 0.5 0.025 1 50
					loop i (listlen $filters) [
						UIBitField (at $filters $i) miniconfilter (<< 1 $i)
					]
				]
			]
		]
	]
]

LoadAudioOptions = [
	replaceui [@arg1] options [
		uialign 0 0
		uivlist 0.01 [
			uialign -1 -1
			uitable 2 0.01 [
				UICheckBox "Stereo" stereo 1 0
				UICheckBox "Mumble Positional Audio" mumble 1 0

				uitext "Sound Volume" 1
				UIHSlider soundvol 0 0 "" 0.5 0.025 1 25
				uitext "Music Volume" 1
				UIHSlider musicvol 0 0 "" 0.5 0.025 1 25
				uitext "Sound Frequency" 1
				UIHListSlider soundfreq "8000 16000 22050 32000 44100" "" 0.5 0.025
				uitext "Sound Channels" 1
				UIHSlider soundchans 0 0 "" 0.5 0.025 1 25
				uitext "Sound bufferlength" 1
				UIHSlider soundbufferlen 0 0 "" 0.5 0.025 32 25
			]

			uispace 0 0.02 [
				UICondButton "Apply Changes" [ pendingchanges ] [applychanges]
			]
		]
	]
]

showresolutions = 0
applyimmediately = 0

//args x, y, children
UIResButton = [
	uitoggle [ && (= $scr_w @arg1) (= $scr_h @arg2) ] [
		if $applyimmediately [
			screenres @@arg1 @@arg2
		] [
			updateval scr_w @@arg1
			updateval scr_h @@arg2
		]
	] .5 [
		uialign -1 0
		uihlist 0.015 [
			uiimage "data/interface/checkbox_false_idle" 0.025 0.025
			uitext (format "%1x%2" @@arg1 @@arg2)
		]
		uihlist 0.015 [
			uiimage "data/interface/checkbox_false_hover" 0.025 0.025
			uitext (format "%1x%2" @@arg1 @@arg2)
		]
		uihlist 0.015 [
			uiimage "data/interface/checkbox_true_idle" 0.025 0.025
			uitext (format "%1x%2" @@arg1 @@arg2)
		]
		uihlist 0.015 [
			uiimage "data/interface/checkbox_true_hover" 0.025 0.025
			uitext (format "%1x%2" @@arg1 @@arg2)
		]
		@arg3
	]
]

LoadVideoOptions = [
	replaceui [@arg1] options [
		uivlist 0.01 [
			uialign -1 -1
			UIExpandable "Screen Setup" showresolutions [
				uivlist 0.01 [
					uihlist 0.02 [
						UICheckBox "Fullscreen" fullscreen 1 0
						UICheckBox "V-Sync" vsync 1 0
					]
					uihlist 0 [
						uitext "Custom Resolution: "
						UIField scr_w 5 () 1 $UIPositiveDecimalFilter
						uitext "x"
						UIField scr_h 5 () 1 $UIPositiveDecimalFilter
						uioffset .05 0 [
							UICondButton "Apply" [ pendingchanges ] [applychanges]
						]
					]
					UICheckBox "Apply immediately" applyimmediately 1 0 [] [
						UITooltip [
							uitext "This option is only available on systems in which the OpenGL context persists despite changes to the window.^nCurrently this is limited to Linux, OS X and freeBSD." .9 .4
						]
						uialign 1 1
					]
					uifill 0 0
					uihlist 0.03 [
						uivlist 0 [
							uitext "5:4" 1.25 0 [uialign 0 0]
							UIResButton 600 480
							UIResButton 1280 1024
							UIResButton 1600 1280
							UIResButton 2560 2048
						]
						uivlist 0 [
							uitext "4:3" 1.25 0 [uialign 0 0]
							UIResButton 320 240
							UIResButton 640 480
							UIResButton 800 600
							UIResButton 1024 768
							UIResButton 1152 864
							UIResButton 1280 960
							UIResButton 1400 1050
							UIResButton 1600 1200
							UIResButton 1792 1344
							UIResButton 1856 1392
							UIResButton 1920 1440
							UIResButton 2048 1536
							UIResButton 3200 2400
						]
						uivlist 0 [
							uitext "16:10" 1.25 0 [uialign 0 0]
							UIResButton 320 200
							UIResButton 640 400
							UIResButton 1024 640
							UIResButton 1280 800
							UIResButton 1440 900
							UIResButton 1600 1000
							UIResButton 1680 1050
							UIResButton 1920 1200
							UIResButton 2048 1280
							UIResButton 2560 1600
							UIResButton 3840 2400
						]
						uivlist 0 [
							uitext "16:9" 1.25 0 [uialign 0 0]
							UIResButton 1024 600
							UIResButton 1280 720
							UIResButton 1366 768
							UIResButton 1600 900
							UIResButton 1920 1080
							UIResButton 2048 1152
							UIResButton 3840 2160
						]
					]
				]
			]
			uihlist 0.01 [
				uitext "Shader Detail"
				uifill 0.04 0
				UIRadioButton "Very Low" shaderdetail 0
				UIRadioButton "Low" shaderdetail 1
				UIRadioButton "Medium" shaderdetail 2
				UIRadioButton "High" shaderdetail 3
			]
			UICheckBox "Bilinear Filtering" bilinear 1 0
			UICheckBox "Trilinear Filtering" trilinear 1 0
			uihlist 0.01 [
				uialign 0 0
				uivlist 0.01 [
					uialign -1 0
					uitext "Gamma" 1
					uitext "Anisotropic Filtering" 1
					uitext "Colour Depth" 1
				]
				uivlist 0.01 [
					UIHSlider gamma 0 0 "" 0.5 0.025 1 50
					UIHListSlider aniso "0 2 4 8 16" "" 0.5 0.025
					UIHListSlider colorbits "0 16 24 32" "" 0.5 0.025
				]
			]
			uispace 0 0.02 [
				uialign 0 0
				UICondButton "Apply Changes" [ pendingchanges ] [applychanges]
			]

			uitable 2 0.01 [
				UICheckBox "SMAA/MLAA" smaa 1 0 () [
					UITooltip [
						uitext "Post-processing filter which detects and smooths lines and edges." 1 .40
					]
				]
				uihlist 0.02 [
					UIRadioButton "Low" smaaquality 0 [smaacoloredge 0]
					UIRadioButton "Medium" smaaquality 1 [smaacoloredge 0]
					UIRadioButton "High" smaaquality 2 [smaacoloredge 0]
					UIRadioButton "Ultra" smaaquality 3 [smaacoloredge 1]
				]

				UICheckBox "FXAA" fxaa 1 0 () [
					UITooltip [
						uitext "Fast Anti-Aliasing method, unfortunately inaccurate. May cause noticeable blurring." .9 .41
					]
				]
				uihlist 0.02 [
					UIRadioButton "Low" fxaaquality 0
					UIRadioButton "Medium" fxaaquality 1
					UIRadioButton "High" fxaaquality 2
					UIRadioButton "Ultra" fxaaquality 3
				]

				UICheckBox "HDR" hdr 1 0
				uifill 0 0

				UICheckBox "Global Illumination" gi 1 0
				uihlist 0.02 [
					UIRadioButton "Low" rhtaps 12
					UIRadioButton "Medium" rhtaps 20
					UIRadioButton "High" rhtaps 32
				]

				UICheckBox "SSAO" ao 1 0
				uifill 0 0

				UICheckBox "Dynamic Lights" dynlights 1 0
				uifill 0 0

				uioffset 0.04 0 [
					uitext "Shadows"
				]
				uihlist 0.02 [
					UIRadioButton "Low" smfilter 0
					UIRadioButton "Medium" smfilter 1
					UIRadioButton "High" smfilter 2
				]

				UICheckBox "Soft Particles" softparticles 1 0
				uifill 0 0

				UICheckBox "Grass" grass 1 0
				uihlist 0.02 [
					uialign -1 0
					UIRadioButton "Low" grassdist 128
					UIRadioButton "Medium" grassdist 256
					UIRadioButton "High" grassdist 512
				]

				UICheckBox "Glass Material: Envmaps" glassenv 1 0
				uihlist 0.02 [
					UIRadioButton "Low" envmapsize 4
					UIRadioButton "Medium" envmapsize 7
					UIRadioButton "High" envmapsize 10
				]

				UICheckBox "Decals" decals 1 0 "" [ uialign -1 0 ]
				uihlist 0.02 [
					UIRadioButton "Medium" decalfade 10000 [maxdecaltris 1024]
					UIRadioButton "High" decalfade 60000 [maxdecaltris 4096]
				]
			]
			uioffset 0 0.05

			uitext "Models" 1.25
			uihlist 0.01 [
				uioffset 0.03 0
				UICheckBox "Bumpmap" bumpmodels 1 0
				UICheckBox "Glow" glowmodels 1 0
				UICheckBox "Envmaps" envmapmodels 1 0
				UICheckBox "Lighting" lightmodels 1 0
			]
			uihlist 0.01 [
				uioffset 0.07 0
				uitext "Animation Quality:" 1
				UIRadioButton "Medium" matskel 1
				UIRadioButton "High" matskel 0
			]
			uitext "Water" 1.25
			uihlist 0.01 [
				uioffset 0.03 0
				UICheckBox "Caustics" caustics 1 0
				UICheckBox "Envmaps" waterenvmap 1 0
				UICheckBox "Reflect" waterreflect 1 0
				UICheckBox "Vertex Anims" vertwater 1 0
			]
			uitext "Waterfall" 1.25
			uihlist 0.01 [
				uioffset 0.03 0
				UICheckBox "Envmaps" waterfallenv 1 0
				UICheckBox "Refraction" waterfallrefract 1 0
			]
		]
	]
]

LoadGameOptions = [
	replaceui [@arg1] options [
		uialign 0 0
		uitext "There are no game options to configure" 1.5
	]
]

LoadEditOptions = [
	replaceui [@arg1] options [
		uialign 0 0
		uivlist 0.01 [
			uialign -1 -1
			uitext "Entities" 1.5
			uihlist 0.05 [
				uivlist 0.01 [
					uitext "Show Helpers" 1
					uitext "	Show Radius" 1
					uitext "	Show Direction" 1
					uitext "	Show Text" 1
					uitext "Show Markers" 1
					uitext "Marker Type" 1
				]
				uivlist 0.01 [
					uihlist 0.01 [
						UIRadioButton "Off" showenthelpers 0
						UIRadioButton "Editmode" showenthelpers 1
						UIRadioButton "Always" showenthelpers 2
					]
					uihlist 0.01 [
						uialign -1 0
						UIRadioButton "Off" showentradius 0
						UIRadioButton "Selection" showentradius 1
						UIRadioButton "Everything" showentradius 2
					]
					uihlist 0.01 [
						uialign -1 0
						UIRadioButton "Off" showentdir 0
						UIRadioButton "Selection" showentdir 1
						UIRadioButton "Everything" showentdir 2
					]
					uihlist 0.01 [
						uialign -1 0
						UIRadioButton "Off" showenttext 0
						UIRadioButton "On" showenttext 1
					]
					uihlist 0.01 [
						uialign -1 0
						UIRadioButton "Editing" showentities 0
						UIRadioButton "Editmode" showentities 1
						UIRadioButton "Always" showentities 2
					]
					UIHSlider entmarkertype 0 0 "" .5 .025 1 100
				]
			]
			UICheckBox "Generate Particles" editingparticles 1 0
			uioffset 0 0.03
			uitext "Renderer" 1.5
			UICheckBox "Wireframe" wireframe 1 0
			UICheckBox "Material Volumes" showmat 1 0
			UICheckBox "Show Selection Grid" showselgrid 1 0
			uioffset 0 0.01
			uihlist 0.01 [
				UICheckBox "Outline" outline 1 0
				UICheckBox "Depth Testing" dtoutline 1 0
			]

			uioffset 0 0.03
			uitext "Misc" 1.5
			uihlist 0.01 [
				uivlist 0.01 [
					uitext "Entity Radius" 1
					uitext "Entity Drop" 1
				]
				uivlist 0.01 [
					UIHSlider entselradius 0 0 "" 0.5 0.025 1 100
					uihlist 0.01 [
						UIRadioButton "Eye" entdrop 0
						UIRadioButton "Ground" entdrop 1
						UIRadioButton "Sel." entdrop 2
						UIRadioButton "Ground to Sel." entdrop 3
					]
				]
			]
			UICheckBox "Apply texture edits to all sides" allfaces 1 0
			UICheckBox "Lookup selection grid size" gridlookup 1 0
			UICheckBox "Entity Snap-to-grid" entselsnap 1 0
			UICheckBox "Entity Surf" entitysurf 1 0
			UICheckBox "Selection Surf" selectionsurf 1 0
		]
	]
]

showeditopts = [
	showmain
	LoadOptions main
	optionsmenu = 4
	LoadEditOptions main
]

LoadAutoexecOptions = [
	replaceui [@arg1] options [
		uialign 0 0
		UITextEditor "autoexec.cfg" -65 28 1
		textinit "autoexec.cfg" "autoexec.cfg"
	]
]

creditsmenu = 0
LoadCredits = [
	replaceui main title [
		uitext "Main Menu - Credits" 1.5
	]
	replaceui $arg1 content [
		uialign 0 0
		uivlist 0.01 [
			uifill 0 0
			uihlist 0.01 [
                UITab creditsmenu 0 "Lamiae" [
                    LoadLamiaeCredits [@@@@@arg1]
                ]
				UITab creditsmenu 1 "Sandbox" [
					LoadSandboxCredits [@@@@@arg1]
				]
				UITab creditsmenu 2 "Cube 2" [
					LoadCube2Credits [@@@@@arg1]
				]
			]
			UIVScroller .9 .75 0.025 .5 [
				uitag credits []
			]
		]
	]
	creditsmenu = 0
	LoadLamiaeCredits [@arg1]
]

LoadLamiaeCredits = [
	replaceui [@arg1] credits [
		uivlist 0.01 [
			uialign 0 0
			uitext "Creator" 1.5
			uitext "Kevin ^"Hirato^" Meyer"
			uioffset 0 .01

			uitext "Lead Programmer" 1.5
			uitext "Kevin ^"Hirato^" Meyer"
			uioffset 0 .01

			uitext "Lead Designer" 1.5
			uitext "Kevin ^"Hirato^" Meyer"
			uioffset 0 .01

			uitext "External Artists" 1.5
			uitext "^"SkiingPenguins^""
			uitext "^"LeoV117^""
			uitext "Craig ^"tyche^" Eddy"
			uitext "Georges ^"TRaK^" Grondin"
			uitext "Richard ^"Ahab^" Honeycutt"
			uitext "Philip Klevestav"
			uitext "Torley @ www.torley.com"
			uitext "http://openFootage.net"
			uioffset 0 .01
		]
	]
]

LoadSandboxCredits = [
	replaceui [@arg1] credits [
		uivlist 0.01 [
			uitext "Creator" 1.5
			uitext "Michael Tomaino"
			uioffset 0 0.01
			uitext "Lead Programmer" 1.5
			uitext "Kevin Meyer"
			uioffset 0 0.01
			uitext "Designer" 1.5
			uitext "Kid Matthew"
			uioffset 0 0.01
			uitext "Programmers" 1.5
			uitext "Thomas Achtner"
			uitext "Dale Weiler"
			uitext "Ilyas Gasanov"
			uioffset 0 0.01
			uitext "Patch Contributers" 1.5
			uitext "Eduardo Terol"
		]
	]
]

LoadCube2Credits = [
	replaceui [@arg1] credits [
		uivlist 0.01 [
			uitext "Lee Salzman"
			uitext "Wouter van Oortmerssen"
			uitext "Robert Pointon"
			uitext "Quinton Reeves"
			uitext "^"Gilt^""
		]
	]
]

UIMenuNoClose "changes" [
	uifill .6 .2 [
		uivlist 0 [
			uialign 0 -1
			uitext "Sandbox must be reinitialised for the following changes to take effect" 1.25
			uispace 0.05 .02 [
				uialign -1 0
				uivlist 0 [
					loopchanges ch [
						uitext $ch 2
					]
				]
			]
			uispace 0.02 0.01 [
				uihlist 0 [
					UIButton "Apply Changes" [ applychanges ]
					uispace 0.02 0
					UIButton "Clear Changes" [ clearchanges ]
				]
			]
		]
	]
] "Reinitialisation Required"

UIMenu "paste" [
	uivlist 0 [
		uitexteditor "#pastebuffer" -60 30 1 ""
		uihlist 0.02 [
			UIBasicButton [
				uitext "Exec"
			] [
				textfocus "#pastebuffer"
				textexec (= (textmark 0) 1)
			]
			UIBasicButton [
				uitext "Clear"
			] [
				textfocus "#pastebuffer"
				textclear
			]
		]
	]

] "Pastebuffer"

notepadfile = "untitled.txt"
UIMenu "notepad" [
	uivlist 0 [
		UIField notepadfile -40 [ shownotepad ] 1 $UISafeFilter [uialign -1 -1]
		UITextEditor $notepadfile -80 30 1
		textinit $notepadfile $notepadfile
	]
] "Notepad"

notepad = [
	notepadfile = $arg1
	shownotepad
]
