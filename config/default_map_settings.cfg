// default settings for maps

// On every map load, this file will be executed.
// If your map has a respective map-art.cfg file
// it will override all of the textures and mapmodels loaded here.

// The -art cfg is saved with your map on every map save


// the skybox for this map:
if $emptymap [
	atmo 1
	sunlightyaw 300
	sunlightpitch 30
	sunlight 224 208 160
]

mapmodelreset // start counting from 0 again
mpacks = ""
loopfiles f config/models cfg [ mpacks = (concat $mpacks (escape $f)) ]
mpacks = (sortlist $mpacks a b [<s $a $b])
looplist t $mpacks [ exec [config/models/@[t].cfg] ]

mapsoundreset //start counting sounds again from 0
spacks = ""
loopfiles f config/mapsounds cfg [ spacks = (concat $spacks (escape $f)) ]
spacks = (sortlist $spacks a b [<s $a $b])
looplist t $spacks [ exec [config/mapsounds/@[t].cfg] ]

materialreset
loop+ i 1 4 [
	texture [glass@i] "textures/glassn.png"    // glass normals

	texture [lava@i] "textures/lava.jpg"
	texture 1 "textures/watern.jpg"        //normals
	texture 1 "textures/lava.jpg"          //fall
	texture 1 "textures/waterfall_n.jpg"

	texture [water@i] "textures/water.png"
	texture 1 "textures/watern.jpg"
	texture 1 "textures/waterfall.jpg"
	texture 1 "textures/waterfall_n.jpg"
]

// texture definitions for the map:
// texture 0 is hardcoded as the sky
// texture 1 hard-coded as the default texture for floors, ceilings, walls, etc

texturereset    // starts the texture slot/index at 0

setshader stdworld   // use default world shader unless specified otherwise

texture 0 "textures/skybox.png" //dummy slot - for the skybox
texture 0 "textures/default.png"

tpacks = ""
loopfiles f config/textures cfg [ tpacks = (concat $tpacks (escape $f)) ]
tpacks = (sortlist $tpacks a b [<s $a $b])
looplist t $tpacks [ exec [config/textures/@[t].cfg] ]

setshader stdworld
