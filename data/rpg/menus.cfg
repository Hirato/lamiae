crosshairs = "default edit"

//entity GUIs

newentgui teledest "Yaw Tag" [0 360 0 0xFFFF]
newentgui jumppad "Z Y X Radius" [-500 500 -500 500 -500 500 0 256]
newentgui checkpoint "Yaw" [0 360]
newentgui spawn "Yaw Radius Tag" [0 360 0 256 0 0xFFFF]
newentgui location "Tag Radius" [0 0xFFFF 0 256]
newentgui blip "" []
newentgui camera "Tag Yaw Pitch Roll" [0 0xFFFF 0 360 -180 180 -180 180]
newentgui platformroute "Tag" [0 0xFFFF]
newentgui critter "Yaw Index" "0 360 0 @(- (r_num_char) 1)"
newentgui obstacle "Yaw Index" "0 360 0 @(- (r_num_obstacle) 1)"
newentgui container "Yaw Index" "0 360 0 @(- (r_num_container) 1)"
newentgui platform "Yaw Index" "0 360 0 @(- (r_num_platform) 1)"
newentgui trigger "Yaw Index" "0 360 0 @(- (r_num_trigger) 1)"
newentgui item "Yaw Index Amount" "0 360 0 @(- (r_num_item) 1) 0 0xFFFF"

//other stuff

games = ""
gamenum = 0
gameind = 0


newgui maps [
	guilist [
		guilist [
			guilist [
				guistayopen [guibutton "Refresh" initmapgui]
				guistrut 15
			]
			guibar
			loop i (- $guiautotab 2) [
				index = (+ $gameind $i)
				if (> $gamenum $index) [
					guibutton (at $games $index) (format "newgame %1" (at $games $index))
				]
			]
			if (! $gamenum) [ guitextbox "No games exist.^nThis should not happen." ]
		]
		guibar
		guiimage $guirollovername $guirolloveraction 4 1 data/sandboxlogo
	]

	guitab "Global Maps"
	guilist [
		guilist [
			genmapitems $globalmaps
		]
		showmapshot
	]

	guitab "My maps"

	mapnum = (listlen $maps)
	mapindex = (max 0 (min $mapindex (- $mapnum (- $guiautotab 2))));

	guilist [
		guilist [
			guilist [
				guistayopen [guibutton "Refresh" initmapgui]
				guistrut 24
			]
			guibar
			loop i (- $guiautotab 2) [
				if (> $mapnum (+ $mapindex $i)) [
					index = (+ $i $mapindex);
					guibutton (at $maps $index) (format "map ^"/%1^"" (at $maps $index))
				]
			]
			if (! $mapnum) [guitext "No custom maps"]
		]
		if (> $mapnum (- $guiautotab 2)) [
			guibar
			guislider mapindex 0 (- $mapnum (- $guiautotab 2)) "" "" 1
		]

		showmapshot
	]
] "games"

creategamelist = [
	games = ""
	loopfiles f data/rpg/games cfg [
		games = (concat $games (format "^"%1^"" $f))
	]
	gamenum = (listlen $games)
]


initmapgui = [
	creategamelist
	createmaplist
]

gameoptions = [
	guitext "Projectiles"
	guilist [
			guicheckbox "allow lensflare	" projallowflare
			guicheckbox "allow lights	" projallowlight
			guicheckbox "maintain direction" firemethod
	]
	guilist [
		guitext "particle emission:	"
		guiradio "sparse" partmul 0.1
		guistrut 1
		guiradio "low" partmul 0.5
		guistrut 1
		guiradio "normal" partmul 1
		guistrut 1
		guiradio "insane" partmul 10
	]
	guilist [
		guitext "Max ricochets:		"
		guiradio "one" maxricochets 1
		guistrut 1
		guiradio "low" maxricochets 5
		guistrut 1
		guiradio "medium" maxricochets 20
		guistrut 1
		guiradio "high" maxricochets 100
	]
	guistrut 1
	guitext "Weapons"
	guilist [
		guicheckbox "Friendly Fire" friendlyfire
	]
	guistrut 1
	guicheckbox "Minimap" minimap
	guicheckbox "Fix orientation" minimapup 0 1
	guilist [
		guitext "Visibility:	"
		guiradio "low" minimapfrac 0.25
		guistrut 1
		guiradio "medium" minimapfrac 0.5
		guistrut 1
		guiradio "full" minimapfrac 1
	]
	guistrut 1
	guitext "Debugging"
	guilist [
		guilist [
			guibitfield "World" debug (<< 1 0)
			guibitfield "Entity" debug (<< 1 1)
		]
		guistrut 1
		guilist [
			guibitfield "Configuration" debug (<< 1 2)
			guibitfield "Projectile" debug (<< 1 3)
		]
		guistrut 1
		guilist [
			guibitfield "Script" debug (<< 1 4)
			guibitfield "AI" debug (<< 1 5)
		]
		guistrut
		guilist [
			guibitfield "I/O" debug (<< 1 6)
			guibitfield "Camera" debug (<< 1 7)
		]
	]
]

numsaves = 0
saves = ""
saveoffset = 0
updatesaves = [
	numsaves = 0
	saves = ""
	loopfiles f data/rpg/saves sgz [
		saves = (concatword $saves " ^"" $f "^"")
		numsaves = (+ $numsaves 1)
	]
]

newgui loadgame [
	guilist [
		guislider saveoffset 0 (- $numsaves (- $guiautotab 2)) "" "" 1
		guilist [
			guitext "select a game		"
			guibar
			if $numsaves [
				loop i (min $guiautotab $numsaves) [
					f = (at $saves (+ $saveoffset $i))
					guibutton $f [loadgame @f]
				]
			] [
				guitext "There are no saves :("
			]
		]
		guibar
		guiimage (concatword "data/rpg/saves/" $guirollovername) [loadgame $guirollovername] 4 1 data/sandboxlogo
	]
] "Load Game"

savename = "untitled"
lasthud = 0

newgui savegame [
	guilist [
		guislider saveoffset 0 (- $numsaves (- $guiautotab 2)) "" "" 1
		guilist [
			guitext "select a game		"
			guibar
			guistayopen [
				if $numsaves [
					loop i (min $guiautotab $numsaves) [
						f = (at $saves (+ $saveoffset $i))
						guibutton $f [savename = [@@f]]
					]
				] [
					guitext "There are no saves :(
				]
			]
		]
		guibar
		guilist [
			guiimage (concatword "data/rpg/saves/" $guirollovername) [loadgame $guirollovername] 4 1 data/sandboxlogo
			guititle "Save Name"
			guifield savename -16
			guibutton "Save" [
				if (>= (listfind f $saves [(strcmp $f $savename)]) 0) [
					showgui overwrite
				] [
					lasthud = $hidehud; hidehud 1; sleep 0 [savegame $savename; hidehud @lasthud]
				]
			]
		]
	]
] "Save Game"

newgui overwrite [
	guitext "You are attempting to overwrite an existing savegame."
	guitext "Are you certain that you wish to proceed?"
	guibar
	guilist [
		guistrut 24
		guibutton "Yes	" [
			lasthud = $hidehud
			hidehud 1
			sleep 0 [
				savegame $savename
				hidehud @lasthud
			]
		]
		guistrut 4
		guibutton "No	" "cleargui 1"
	]
] "Overwrite Save Game?"

scriptindex = 0
effectindex = 0
statusindex = 0
itemindex = 0
item_useindex = 0
ammoindex = 0
charindex = 0
factionindex = 0
containeridx = 0
triggeridx = 0
platformidx = 0
obstacleidx = 0
mapscriptindex = 0
recipeindex = 0

//deffield type field
deffield = [
	guilist [
		(concatword "r_select_" $arg1) $(concatword $arg1 "index") [
			(concatword $arg1 $arg2) = ((concatword "r_" $arg1 "_" $arg2 "_get"))
		]
		guitext (tabify (concatword $arg2 ":") 3)
		guifield (concatword $arg1 $arg2) -14 [
			(concatword "r_select_" @arg1) $(concatword @arg1 "index") [
				@(concat (concatword "r_" @@@arg1 "_" @@@arg2) $(concatword @@@arg1 @@@arg2))
			]
		]
	]
]

//specialised for uses; requires an extra select
deffielduse = [
	guilist [
		r_select_item $itemindex [
			r_select_item_use $item_useindex [
				(concatword "item_use" $arg1) = ((concatword "r_item_use_" $arg1 "_get"))
			]
		]
		guitext (tabify (concatword $arg1 ":") 3)
		guifield (concatword "item_use" $arg1) - 14 [
			r_select_item $itemindex [
				r_select_item_use $item_useindex [
					(concatword "r_item_use_" @@@arg1) $(concatword "item_use" @@@arg1)
				]
			]
		]
	]
]

defnav = [
	guilist [
		guistayopen [
			guibutton "back" [
				(concatword @arg1 "index") = (- $(concatword @arg1 "index") 1)
				clamp (concatword @arg1 "index") 0 (- @arg2 1)
			]
			guistrut 1
			guititle (concat $(concatword $arg1 "index") / (- ((concatword "r_num_" $arg1)) 1))
			guistrut 1
			guibutton "next" [
				(concatword @arg1 "index") = (+ $(concatword @arg1 "index") 1)
				clamp (concatword @arg1 "index") 0 (- @arg2 1)
			]
		]
	]
]

newgui definitions [
	guitextbox "Note that many things cannot be changed in here and that none of them are permanent, changes will persist till the end of the session with a few notable exceptions.^n#1 all changes to the player will persist.^n#2 changes to object/critter definitons will not persist, but creatures/objects spawned with the new attributes will." 60

	//guitab "scripts"

	guitab "effects"
	defnav effect (r_num_effect)
	deffield effect flags
	deffield effect decal
	deffield effect mdl
	deffield effect particle
	deffield effect colour
	deffield effect fade
	deffield effect gravity
	deffield effect size
	deffield effect lightflags
	deffield effect lightfade
	deffield effect lightradius
	deffield effect lightinitradius
	deffield effect lightcol
	deffield effect lightinitcol

	guitab "statuses"
	defnav status (r_num_status)
	deffield status friendly
	deffield status icon
	deffield status name
	deffield status description

	guitab "items"
	guilist [
		guilist [
			defnav item (r_num_item)
		]
		guititle "			"
		guilist [
			r_select_item $itemindex [
				defnav item_use (r_num_item_use)
			]
		]
	]

	guilist [
		guilist [
			deffield item name
			deffield item icon
			deffield item description
			deffield item mdl
			deffield item script
			deffield item type
			deffield item flags
			deffield item worth
			deffield item weight
			deffield item cursebase
		]
		guilist [
			deffielduse reqs_strength
			deffielduse reqs_endurance
			deffielduse reqs_agility
			deffielduse reqs_charisma
			deffielduse reqs_wisdom
			deffielduse reqs_intelligence
			deffielduse reqs_luck
			deffielduse reqs_armour
			deffielduse reqs_diplomacy
			deffielduse reqs_magic
			deffielduse reqs_marksman
			deffielduse reqs_melee
			deffielduse reqs_stealth
			deffielduse reqs_craft
		]
		guilist [

		]
	]

	guitab "ammotypes"
	defnav ammo (r_num_ammo)
	deffield ammo name

	guitab "critters"
	guilist [
		guistayopen [
			guibutton "Player" [charindex = (? (strcmp $charindex "player") 0 "player")] (? (strcmp $charindex "player") "checkbox_on" "checkbox_off")
		]
		guistrut 1
		guilist [
			defnav char (r_num_char)
		]
	]
	guilist [
		guilist [
			deffield char name
			deffield char mdl
			deffield char script
			deffield char faction
		]
	]

	guitab "faction"
	defnav faction (r_num_faction)
	deffield faction name
	deffield faction logo

//	guitab "containers"
//	guitab "triggers"
//	guitab "obstacles"
//	guitab "platforms"

	//guitab "mapscripts"

	guitab "recipes"
	defnav recipe (r_num_recipe)
	deffield recipe flags
	deffield recipe reqs_strength
	deffield recipe reqs_endurance
	deffield recipe reqs_agility
	deffield recipe reqs_charisma
	deffield recipe reqs_wisdom
	deffield recipe reqs_intelligence
	deffield recipe reqs_luck
	deffield recipe reqs_armour
	deffield recipe reqs_diplomacy
	deffield recipe reqs_magic
	deffield recipe reqs_marksman
	deffield recipe reqs_melee
	deffield recipe reqs_stealth
	deffield recipe reqs_craft

] "note"

newgui main [
	guibutton "Load Game" [updatesaves; showgui loadgame]
	guibutton "Save Game" [updatesaves; showgui savegame]
	if (! $mainmenu) [
		guilist [
			if $editing [
				guibutton "Browse definitions" "showgui definitions"
				guistrut 1
			]
			guibutton "Player GUI (TAB)" [showplayergui 1]
		]
	]
	guibar
	@main
]

newgui chat [
	guitextbox (r_get_dialogue) 60 -5
	guistrut 1
	guistayopen [
		loop i (r_num_response) [
			guibutton (concatword (+ $i 1) ") " (r_get_response $i)) [r_trigger_response @i]
		]
	]
]

newgui loot [

]

invenoffset = 0
useoffset = 0
equipoffset = 0
dropamount = 1

slots = ["Left hand" "Right hand" "Legs" "Arms" "Torso" "Head" "Feet" "Quiver"]
stats = [Strength Endurance Agility Charisma Wisdom Intelligence Luck]
skills = [Armour Diplomancy Magic Marksman Melee Stealth Craft]
elements = [ "Fire" "Water" "Air" "Earth" "Magic" "Slash" "Blunt" "Pierce" ]

showplayergui = [
	cond $mainmenu [
		echo "The player gui cannot be displayed in the main menu"
	] $numargs [
		updateinv
		showgui playergui
	] [! (cleargui 1)] [
		updateinv
		showgui playergui
	]
]

newgui examine [
	if (= (r_reftype examine) $REF_INV) [
		guistayopen [
			r_select_item examine [
				guilist [
					guilist [
						guiimage (concatword $ICONPATH (? (r_item_icon_get) (r_item_icon_get) "default")) [] 4 0 data/rpg/logo
						guitext (r_item_name_get)
						guitext (concat Amount: (r_get_amount examine))
						guitext (concat Value: (* (r_get_amount examine) (r_item_worth_get)))
						guitext (concat "	each:" (r_item_worth_get))
						guitext (concat Weight: (r_get_weight examine))
						guitext (concat "	each:" (r_item_weight_get))
					]
					guibar
					if (r_num_item_use) [
						guilist [
							guititle "Uses"
							loop i (r_num_item_use) [
								r_select_item_use $i [
									guitext (concatword (+ $i 1) ") " (at "Consume Equip Wield" (r_item_use_type_get)) ": "(r_item_use_name_get))
								]
							]
						]
						guibar
					]
					guitextbox (r_item_description_get) 40 16
				]
				loop i (r_num_item_use) [
					guitab (concat Use: (+ $i 1))
					r_select_item_use $i [

					]
				]
			]
		]
	] [
		guitext "You need to select an item first"
	]
] Examine

numitems = 0

updateinv = [
	loop i (+ $numitems 1) [
		r_registerref (concatword "item" $i)
		r_setref (concatword "item" $i) //nulls them out
	]
	numitems = 0
	r_loop_inv player item [
		r_registerref (concatword "item" $i)
		r_setref (concatword "item" $numitems) item
		numitems = (+ $numitems 1)
	]
]

newgui hotkey [
	guionclear [hotkey_set_pending -1 -1]
	guitextbox "Press a hotkey combination to use for the item and use and then close this menu" -40
]

newgui playergui [
	guistayopen [
		guilist [
			guilist [
				loop i $STAT_MAX [
					guitext (concatword (at $stats $i) ": " (r_get_attr player $i))
				]
				guistrut 1
				loop i $SKILL_MAX [
					guitext (concatword (at $skills $i) ": " (r_get_skill player $i))
				]
				guistrut 1
				guitext (concat Level: (r_get_level player))
				guitext (concat Experience: (r_get_exp player))
				guitext (concat Needed EXP: (r_get_neededexp player))
				guitext (concat Skill Points: (r_get_points player))
			]
			guititle "	"
			guilist [
				loop i $STAT_MAX [
					guilist [
						guibutton "" [r_invest_attr player @i] //"+1"
						//guibutton "" [loop j 5 [r_invest_attr player @@i]] "+5"
						//guibutton "" [loop j 10 [r_invest_attr player @@i]] "+10"
					]
				]
				guistrut 1
				loop i $SKILL_MAX [
					guilist [
						guibutton "" [r_invest_skill player @i] //"+1"
						//guibutton "" [loop j 5 [r_invest_skill player @@i]] "+5"
						//guibutton "" [loop j 10 [r_invest_skill player @@i]] "+10"
					]
				]
			]
			guistrut 2
			guilist [
				guitext (concat Health: (r_get_health player) / (r_get_maxhealth player))
				guitext (concat H. regen (r_get_healthregen player) / sec)
				guitext (concat Mana: (r_get_mana player) / (r_get_maxmana player))
				guitext (concat M. regen (r_get_manaregen player) / sec)
				guitext (concat Weight: (r_get_weight player) / (r_get_maxweight player))
				guitext (concat Movespeed: (r_get_maxspeed player))
				guitext (concat Jump Vel: (r_get_jumpvel player))
				guistrut 1
				guitext "Type		Res.	Th."
				loop i $ATTACK_MAX [
					guitext (format "%1%2	%3" (tabify (at $elements $i) 3) (r_get_resist player $i) (r_get_thresh player $i))
				]
			]
		]
		guitab "Inventory"

		guilist [
			guilist [
				if (r_reftype "examine") [
					r_select_item examine [
						guitext (r_item_name_get)
						guitext (concat Amount: (r_get_amount examine))
						guitext (format "Weight: %1 (%2)" (r_get_weight examine) (r_item_weight_get))
						guistrut 1
						guitextbox (r_item_description_get) 30 6

						// 10 lines for actions; 2 goes to examine and drop
						guibutton "Examine" [showgui examine]
						guilist [
							guibutton "Drop: " [r_drop player @(r_get_base examine) $dropamount; updateinv]
							guifield dropamount -5 [dropamount = (max $dropamount 1)]
						]
						clamp useoffset 0 (- (r_num_item_use) 8)
						guilist [
							guilist [
								loop i (min 8 (r_num_item_use)) [
									idx = (+ $i $useoffset)
									r_select_item_use $idx [
										case (r_item_use_type_get) $USE_CONSUME [
											guitext (concat Consume: (r_item_use_name_get))
										] $USE_ARMOUR [
											guibutton (concat Equip: (r_item_use_name_get)) [ r_equip player @(r_get_base examine) @idx; updateinv]
										] $USE_WEAPON [
											guibutton (concat Wield: (r_item_use_name_get)) [ r_equip player @(r_get_base examine) @idx; updateinv]
										]
									]
								]
							]
							if (> (r_num_item_use) 8) [
								guislider useoffset 0 (- (r_num_item_use) 8) "" "" 1
							]
						]

					]
				] [
					guitextbox "select an item first" 30 14
				]
			]
			clamp invenoffset 0 (- (r_inv_numitems) 25)
			guilist [
				loop i 5 [
					guilist [
						loop j 5 [
							guilist [
								item = (concatword "item" (+ $invenoffset (+ (* 5 $i) $j)))
								if (r_reftype $item) [
									r_select_item $item [
										guiimage (concatword $ICONPATH (? (r_item_icon_get) (r_item_icon_get) "default")) [
											r_setref examine @item
										] 1.5 0 data/rpg/logo.png
										guititle (r_get_amount $item)
									]
								] [
									guiimage (concatword $ICONPATH "empty") [] 1.5 0 data/rpg/logo.png
									guititle ""
								]
							]
						]
					]
				]
			]
			guislider invenoffset 0 (- (r_inv_numitems) 25) [invenoffset = (* 5 (div $invenoffset 5))] 0 1
		]

		guitab "Equipment"

		r_loop_equip player equip [
			r_select_item equip [
				r_select_item_use equip [
					guibutton (concat (r_item_name_get) - (r_item_use_name_get)) [
						r_dequip player @(r_get_base equip) @(r_item_use_slots_get)
						updateinv
					]
				]
			]
		]
		guitab "Effects"

		guitab "Crafting"
		loop i (r_num_recipe) [
			r_select_recipe $i [
				if (& (r_recipe_flags_get) $RECIPE_KNOWN) [
					guibutton (concat Recipe: (+ $i 1)) [r_use_recipe @i; updateinv]
				]
			]
		]
		guitab "Journal"
	]
] "Stats"
