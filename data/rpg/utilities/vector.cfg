// Basic vector library

do [
	local ops
	ops = [
		add +f
		sub -f
		mul *f
		div divf
		min minf
		max maxf
	]

	loop i (divf (listlen $ops) 2) [
		do [
			r_vec_@(at $ops (* $i 2)) = [
				? (= (listlen $arg2) 1) [
					@(@@@(at $ops (+ (* $i 2) 1)) (at $arg1 0) $arg2)
					@(@@@(at $ops (+ (* $i 2) 1)) (at $arg1 1) $arg2)
					@(@@@(at $ops (+ (* $i 2) 1)) (at $arg1 2) $arg2)
				] [
					@(@@@(at $ops (+ (* $i 2) 1)) (at $arg1 0) (at $arg2 0))
					@(@@@(at $ops (+ (* $i 2) 1)) (at $arg1 1) (at $arg2 1))
					@(@@@(at $ops (+ (* $i 2) 1)) (at $arg1 2) (at $arg2 2))
				]
			]
		]
	]
]

r_vec_squaredlen = [
	+f (pow (at $arg1 0) 2) (pow (at $arg1 1) 2) (pow (at $arg1 2) 2)
]

r_vec_magnitude = [
	sqrt (@r_vec_squaredlen)
]

r_vec_rescale = [
	r_vec_div $arg1 (*f (r_vec_magnitude $arg1) $arg2)
]

r_vec_normalise = [
	r_vec_div $arg1 (r_vec_magnitude $arg1)
]

r_vec_dot = [
	+f (*f (at $arg1 0) (at $arg2 0)) (*f (at $arg1 1) (at $arg2 1)) (*f (at $arg1 2) (at $arg2 2))
]

r_vec_dist = [
	r_vec_magnitude (r_vec_sub $arg1 $arg2)
]



// Some RPG helpers

r_vec_distent = [
	local _ao _bo
	r_select_ent $arg1 [ _ao = $r_ent_o ]
	r_select_ent $arg2 [ _bo = $r_ent_o ]
	r_vec_dist $_ao $_bo
]
