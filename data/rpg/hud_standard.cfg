hudiconname = [
	local ret
	ret = "default"
	loopwhile i $numargs [ strcmp $ret "default" ] [
		ind = (+ $i 1)
		do [
			if (strlen $arg@ind) [
				ret = $arg@ind
			]
		]
	]

	result $ret
]

hudequip = [
	//weapon, ammo, x, y
	local SIZE OFFSET
	SIZE = 160
	OFFSET = (+ $SIZE 16)

	col = (? (> (r_get_lastaction player) $lastmillis) 0x7F7F7F 0xFFFFFF)
	if (= (r_reftype $arg1) $REF_EQUIP) [
		r_select_item $arg1 [ r_select_item_use $arg1 [
			if (&& (>= (r_item_use_type_get) $USE_WEAPON) [r_item_use_cost_get]) [
				r_hud_image (r_iconpath "empty") $arg3 $arg4 $SIZE $SIZE $col
				r_hud_image (r_iconpath (hudiconname (r_item_use_icon_get) (r_item_icon_get))) $arg3 $arg4 $SIZE $SIZE $col

				amount = 0
				r_hud_image (r_iconpath "empty") (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col

				case (r_item_use_ammo_get) -3 [ //experience
					r_select_char player [ amount = (r_char_base_experience_get) ]
					if (< $amount (r_item_use_cost_get)) [ col = 0xFF7F7F ]
					r_hud_image (r_iconpath "exp") (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
				] -2 [ //health
					amount = (r_get_health player)
					if (< $amount (r_item_use_cost_get)) [ col = 0xFF7F7F ]
					r_hud_image (r_iconpath "health") (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
				] -1 [ //mana
					amount = (r_get_mana player)
					if (< $amount (r_item_use_cost_get)) [ col = 0xFF7F7F ]
					r_hud_image (r_iconpath "mana") (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
				] () [ //default
					valid = 0
					r_select_ammo (r_item_use_ammo_get) [
						loopwhile i (r_ammo_num_item) [ = $valid 0 ] [
							if (= (r_get_base $arg1) (r_ammo_get_item $i)) [
								valid = 2
								amount = (r_get_amount player (r_get_base $arg1))
							]
							if (= (r_reftype $arg2) $REF_EQUIP) [
								if (= (r_get_base $arg2) (r_ammo_get_item $i)) [
									valid = 1
									amount = (r_get_amount player (r_get_base $arg2))
								]
							]
						]
					]

					if (< $amount (r_item_use_cost_get)) [ col = 0xFF7F7F ]

					case $valid 0 [
						r_hud_image (r_iconpath "invalid") (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
					] 1 [
						r_select_item $arg2 [
							r_select_item_use $arg2 [
								r_hud_image (r_iconpath (hudiconname (r_item_use_icon_get) (r_item_icon_get))) (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
							]
						]
					] 2 [
						r_hud_image (r_iconpath (hudiconname (r_item_use_icon_get) (r_item_icon_get))) (+f $arg3 $OFFSET) $arg4 $SIZE $SIZE $col
					]
				]

				tw = (divf (text_width (* $amount 1)) 2)
				r_hud_text (- $arg3 -256 $tw) (+ $arg4 16) 1 $col (* $amount 1)

				tw = (divf (text_width (concatword "-" (r_item_use_cost_get))) 2)
				r_hud_text (- $arg3 -256 $tw) (+ $arg4 84) 1 $col (concatword "-" (r_item_use_cost_get))

			] [
				r_hud_image (r_iconpath "empty") (+f $arg3 112) $arg4 $SIZE $SIZE $col
				r_hud_image (r_iconpath (hudiconname (r_item_use_icon_get) (r_item_icon_get))) (+f $arg3 112) $arg4 $SIZE $SIZE $col
			]
		]]
	] [
		r_hud_image (r_iconpath "empty") (+ $arg3 96) $arg4 $SIZE $SIZE $col
	]
]

hudcharge = [ //attacking weapon, owner, x, y, dx, dy
	progress = 0
	threshold = 0
	if (= (r_reftype $arg1) $REF_EQUIP) [
		r_select_item_use $arg1 [
			threshold = (divf (r_item_use_mincharge_get) (r_item_use_maxcharge_get))
			progress = (divf (r_get_charge $arg2) (*f (r_item_use_charge_get) (r_item_use_maxcharge_get)))
		]
	]

	r_hud_image (r_iconpath "hback") $arg3 $arg4 $arg5 $arg6 0x0
	r_hud_horizbar (r_iconpath "hbar") $arg3 $arg4 $arg5 $arg6 $progress (? (>f $progress $threshold) 0x00FF00 0xFF0000)

	r_hud_solid (+f $arg3 (*f $threshold $arg5) -3) $arg4 6 $arg6 0xFFFF00
]

draweffects = [ //efx string, y
	x = (-f (divf $hud_right 2) (*f 36 (listlen $arg1)))
	looplist img $arg1 [
		r_hud_image (r_iconpath $img) $x $arg2 64 64 0xFFFFFF
		x = (+f $x 72)
	]
]

statusbar = [ //x, y, dx, dy, cur, max, colour, pulse, left (text)
	local x y dx dy col backcol
	dx = $arg3
	dy = $arg4
	col = $arg7

	if (&& $arg8 [<f (divf $arg5 $arg6) .25]) [ //pulse
		x = (+f $arg1 (*f 8 (sin $lastmillis)))
		y = (+f $arg2 (*f 8 (cos $lastmillis)))
		backcol = 0x0
		if (& $col 0xFF) [
			backcol = (+ 127 (*f 127.5 (sin $lastmillis)))
		]
		if (& $col 0xFF00) [
			backcol = (| $backcol (<< (+ 127 (*f 127.5 (sin $lastmillis))) 8))
		]
		if (& $col 0xFF0000) [
			backcol = (| $backcol (<< (+ 127 (*f 127.5 (sin $lastmillis))) 16))
		]
	] [
		x = $arg1
		y = $arg2
		backcol = (| (>> (& $col 0xFF) 2) (<< (>> (& $col 0xFF00) 10) 8) (<< (>> (& $col 0xFF0000) 18) 16))
	]

	r_hud_image (r_iconpath "hback") $x $y $dx $dy $backcol
	r_hud_horizbar (r_iconpath "hbar") $x $y $dx $dy (divf $arg5 $arg6) $col

	if $arg9 [
		r_hud_text (+f $x (*f $dx 0.125)) $y (divf $dy 64) (& (~ $backcol) 0xFFFFFF) (* 1 $arg5)
	] [
		local text offset
		text = (* 1 $arg5)
		offset = (*f (text_width $text) (divf $dy 64))
		r_hud_text (-f (+f $x (*f $dx 0.125)) $offset) $y (divf $dy 64) (& (~ $backcol) 0xFFFFFF) (* 1 $arg5)
	]
]

drawhotkey = [ //x, y, dx, dy, idx
	r_hud_image (r_iconpath "empty") $arg1 $arg2 $arg3 $arg4 0xFFFFFF
// 	idx = (at (hotkey_get $arg5) 0)
// 	use = (at (hotkey_get $arg5) 1)
//
// 	if (> $idx 0) [
// 		image = "default"
// 		r_select_item $idx [
// // 			r_select_item_use $idx [
// // 				if (r_item_use_icon_get)
// //
// // 			]
// 			if (r_item_icon_get) [
// 				image = (r_item_icon_get)
// 			]
// 		]
//
// 		r_hud_image (r_iconpath $image) $arg1 $arg2 $arg3 $arg4 (? (> (r_get_amount player $idx) 0) 0xFFFFFF 0x3F3F3F)
// 	]
//
	r_hud_text (-f $arg1 8) $arg2 .5 0xFFFFFF (+ $arg5 1)
]

//redefine to add some extra stuff onto the HUD
hudextra = []

r_hud [
	if (!= (r_get_state player) $CS_DEAD) [
		if (!= (r_reftype hover) $REF_INVALID) [
			local str
			case (r_reftype hover) $REF_ITEM [
				r_select_item hover [
					str = (format "%1 (%2)" (r_item_name_get) (r_item_quantity_get))
				]
			] $REF_CONTAINER [
				if (|| (= (r_get_faction hover) -1) [= (r_get_faction player) (r_get_faction hover)] [r_hostile player hover]) [
					str = (r_get_name hover -1)
				] [
					//not yours and faction friendly - warn
					str = (concatword "^f3" (r_get_name hover -1))
				]
			] () [
				str = (r_get_name hover -1)
			]

			r_hud_text (divf (-f $hud_right (text_width $str)) 2) (+f (divf $hud_bottom 2) 16)  1 0xFFFFFF $str
		]
		r_hud_minimap player (- $hud_right 288) (- $hud_bottom 456) 256 256

		statusbar (-f (divf $hud_right 2) 5) (- $hud_bottom 144) -445 48 (r_get_health player) (r_get_maxhealth player) 0xFF0000 1 0
		statusbar (+f (divf $hud_right 2) 5) (- $hud_bottom 144) 445 48 (r_get_mana player) (r_get_maxmana player) 0x007FFF 0 1

		r_stack [
			r_local eleft eright quiver attack

			r_setref_equip eleft player $SLOT_LHAND
			r_setref_equip eright player $SLOT_RHAND
			r_setref_equip quiver player $SLOT_QUIVER

			if (r_get_primary player) [ r_setref attack eleft ]
			if (r_get_secondary player) [ r_setref attack eright ]

			hudequip eleft quiver 16 (-f $hud_bottom 176)
			hudequip eright quiver (-f $hud_right 352) (-f $hud_bottom 176)

			if (r_get_charge player) [
				hudcharge attack player (-f (divf $hud_right 2) 256) (-f $hud_bottom 240) 512 64
			]
		]

		enc = (divf (r_get_weight player) (r_get_maxweight player))
		if (>f $enc 1) [
		//ICOMMAND(r_hud_image, "sffffi", (const char *i, float *x, float *y, float *dx, float *dy, int *c),
			size = (*f 128 (-f $enc 1))
			r_hud_image (r_iconpath "weight") (-f $hud_right $size 160) (-f $hud_bottom (*f $size 2) 460) (*f $size 2) (*f $size 2) 0xFFFFFF
		]

		veffects = ""
		r_loop_veffects player efx [
			if (> (r_get_status_duration efx -1) 500) [
				r_select_status efx [
					icon = (r_status_icon_get)
					if (strcmp $icon "") [icon = "default"]
					veffects = (concatword $veffects " ^"" (r_status_icon_get) "^"")
				]
			]
		]
		draweffects $veffects 16

		aeffects = ""
		r_loop_aeffects curmap player efx [
			if (> (r_get_status_duration efx -1) 500) [
				r_select_status efx [
					icon = (r_status_icon_get)
					if (strcmp $icon "") [icon = "default"]
					aeffects = (concatword $aeffects " ^"" (r_status_icon_get) "^"")
				]
			]
		]
		draweffects $aeffects 96

		loop i 10 [
			index = (+ $i (? $shiftkey 20 0) (? $ctrlkey 10 0))
			drawhotkey (+f (divf $hud_right 2) -445 (* $i 90)) (-f $hud_bottom 96) 80 80 $index
		]
	]

	hudextra
]
