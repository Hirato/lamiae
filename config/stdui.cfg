//ui templates to simplify UI creation and ease maintainability
//these should be used instead of the raw commands to keep things consistent and easily changeable.
//All fo these are prefixed with UI

//Quick n' Easy means of displaying state - just use as a child
UIIdle =       [ uifill 0 0 [uiclamp 1 1 1 1] ]
UIHover =      [ uimodcolor 0xCFCFCF 0 0 [uiclamp 1 1 1 1]]
UISelected =   [ uimodcolor 0x8F8F8F 0 0 [uiclamp 1 1 1 1]]
UIDisabled =   [ uimodcolor 0x4F4F4F 0 0 [uiclamp 1 1 1 1]]
UIFalse =      [ uimodcolor 0xAFAFAF 0 0 [uiclamp 1 1 1 1]]
UIFalseHover = [ uimodcolor 0x7F7F7F 0 0 [uiclamp 1 1 1 1]]
UITrue =       [ uifill 0 0 [uiclamp 1 1 1 1]]
UITrueHover =  [ uimodcolor 0xCFCFCF 0 0 [uiclamp 1 1 1 1]]

//Creates a basic menu with a title and a close button
//args: name, contents, title, onhide, children,
UIMenu = [
	[show@arg1] = [
		showui [@@arg1] [
			uivlist 0 [
				uistretchedimage "media/interface/title" 0.04 0.04 [
					uiclamp 1 1 0 0
					uitag title [
						uialign 0 0
						uitext [@@@@@@arg3] 1.5
					]
					uispace 0.01 0 [
						uialign 1 0
						UICloseButton [@@@@@@arg1]
					]
				]
				uistretchedimage "media/interface/body" 0.04 0.04 [
					uiclamp 1 1 0 0
					uivlist 0 [
						uispace 0.01 0 [
							@@@@@@arg2
						]
						uioffset 0. 0.01
					]
				]
			]
			@@(if (>= $numargs 5) [
				result [@@arg5]
			])
		] [@@arg4]
	]
]

//like the above, but without a close button
//args: name, contents, title, onhide, children,
UIMenuNoClose = [
	[show@arg1] = [
		showui [@@arg1] [
			uivlist 0 [
				uistretchedimage "media/interface/title" 0.04 0.04 [
					uiclamp 1 1 0 0
					uitag title [
						uialign 0 0
						uitext [@@@@@@arg3] 1.5
					]
				]
				uistretchedimage "media/interface/body" 0.04 0.04 [
					uiclamp 1 1 0 0
					uivlist 0 [
						uispace 0.01 0 [
							@@@@@@arg2
						]
						uioffset 0. 0.01
					]
				]
			]
			@@(if (>= $numargs 5) [
				result [@@arg5]
			])
		] [@@arg4]
	]
]

//like the above, but without a title
//args: name, contents, onhide, children
UIMenuNoTitle = [
	[show@arg1] = [
		showui [@@arg1] [
			uistretchedimage "media/interface/body_full" 0.04 0.04 [
				uiclamp 1 1 0 0
				uivlist 0 [
					uispace 0.01 0 [
						@@@@@arg2
					]
					uioffset 0. 0.01
				]
			]
			@@(if (>= $numargs 4) [
				result [@@arg4]
			])
		] [@@arg3]
	]
]

//Creates a UI that cannot be interacted with in any way.
//regular UI rules apply, but these will not receive any input and
//won't cause the mouse to be freed with freecursor 1
//args: name contents
UIOverlay = [
	[show@arg1] = [
		showui [@@arg1] [
			uistretchedimage "media/interface/body_full" 0.04 0.04 [
				//no borders, no, nothing!
				@@@arg2
			]
		] "" 1
	]
]

// Creates a generic, empty tooltip with a background.
// These can only be added to buttons
// args: contents
UITooltip = [
	uitooltip [
		uistretchedimage "media/interface/tooltip" .04 .04 [
			uispace 0.01 0.01 [
				@@@arg1
			]
		]
	]
]

//Creates a simple close button
//args: menu
UICloseButton = [
	uibutton [ hideui [@@arg1] ] [
		uialign 1 0
		uiimage "media/interface/menu_close" 0.03 0.03 UIIdle
		uiimage "media/interface/menu_close" 0.03 0.03 UIHover
		uiimage "media/interface/menu_close" 0.03 0.03 UISelected
	]
]

//Creates an iamge object that loads on mouse over, uses the logo as a fallback.
UIThumbnail = [
	uithumbnail $arg1 $arg2 $arg3 [
		uialtimage "media/interface/lamiae"
	]
]

//creates a button based on the arguments provided by the caller.
//args: contents, action, children
UIBasicButton = [
	uifill 0 0 [
		arg1
		uibutton [ @@arg2 ] [
			uiclamp 1 1 1 1
			UIIdle
			UIHover
			UISelected
			@@arg3
		]
	]
]

//creates a button with a centred text element
//args: text, action, children
UIButton = [
	uibutton [ @arg2 ] [
		uiclamp 1 1 0 0
		uistretchedimage "media/interface/button_idle" 0.16 0.04 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1.2
			]
		]
		uistretchedimage "media/interface/button_hover" 0.16 0.04 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1.2
			]
		]
		uistretchedimage "media/interface/button_selected" 0.16 0.04 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1.2
			]
		]
		@arg3
	]
]

//creates a button with a centred text element - smaller version of the above
//args: text, action, children
UISmallButton = [
	uibutton [ @arg2 ] [
		uiclamp 1 1 0 0
		uistretchedimage "media/interface/button_idle" 0.12 0.03 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1
			]
		]
		uistretchedimage "media/interface/button_hover" 0.12 0.03 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1
			]
		]
		uistretchedimage "media/interface/button_selected" 0.12 0.03 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1
			]
		]
		@arg3
	]
]

//creates a conditional button with a centred text element
//args: text, cond, action, children
UICondButton = [
	uicondbutton [ @arg2 ] [ @arg3 ] [
		uiclamp 1 1 0 0
		uistretchedimage "media/interface/button_disabled" 0.16 0.04 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1.2
			]
			UIDisabled
		]
		uistretchedimage "media/interface/button_idle" 0.16 0.04 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1.2
			]
		]
		uistretchedimage "media/interface/button_hover" 0.16 0.04 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1.2
			]
		]
		uistretchedimage "media/interface/button_selected" 0.16 0.04 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1.2
			]
		]
		@arg4
	]
]

//creates a conditional button with a centred text element - smaller version of the above
//args: text, cond, action, children
UISmallCondButton = [
	uicondbutton [ @arg2 ] [ @arg3 ] [
		uiclamp 1 1 0 0
		uistretchedimage "media/interface/button_disabled" 0.12 0.03 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1
			]
			UIDisabled
		]
		uistretchedimage "media/interface/button_idle" 0.12 0.03 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1
			]
		]
		uistretchedimage "media/interface/button_hover" 0.12 0.03 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1
			]
		]
		uistretchedimage "media/interface/button_selected" 0.12 0.03 [
			uiclamp 1 1 0 0
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg1] 1
			]
		]
		@arg4
	]
]

//Creates a tab
//args: var, trueval, text, action
UITab = [
	uitoggle [ = $@arg1 @arg2 ] [
		updateval @arg1 [@@arg2]
		@arg4
	] 0.5 [
		uistretchedimage "media/interface/tab_idle" 0.12 0.0325 [
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg3] 1.1
			]
		]
		uistretchedimage "media/interface/tab_hover" 0.12 0.0325 [
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg3] 1.1
			]
		]
		uistretchedimage "media/interface/tab_selected" 0.12 0.0325 [
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg3] 1.1
			]
		]
		uistretchedimage "media/interface/tab_selected_hover" 0.12 0.0325 [
			uispace 0.01 0 [
				uialign 0 0
				uitext [@@@arg3] 1.1
			]
		]
	]
]

//Creates a tab with user specified content
//args: var, trueval, content, action
UIBasicTab = [
	uitoggle [ = $@arg1 @arg2 ] [
		updateval @arg1 [@@arg2]
		@arg4
	] 0.5 [
		uifill 0 0 [
			@@arg3
			UIFalse
		]
		uifill 0 0 [
			@@arg3
			UIFalseHover
		]
		uifill 0 0 [
			@@arg3
			UITrue
		]
		uifill 0 0 [
			@@arg3
			UITrueHover
		]
	]
]

//Creates a simple checkbox with a text element on the right
//args: Text, Var, trueval, falseval, OnChange, children
UICheckBox = [
    if (< $numargs 5) [arg5 = ""]
	uitoggle [ || (=f $@arg2 @arg3) (!=f $@arg2 @arg4) ] [
		updateval @arg2 (? (|| (=f $@arg2 @arg3) (!=f $@arg2 @arg4)) @arg4 @arg3) [@@arg5]
	] 0.5 [
		uialign -1 0
		uihlist 0.015 [
			uiimage "media/interface/checkbox_false_idle" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/checkbox_false_hover" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/checkbox_true_idle" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/checkbox_true_hover" 0.025 0.025
			uitext [@@@arg1] 1
		]
		if (>= $numargs 6) [
			@@arg6
		]
	]
]

//Like the above, but for a bitfield
//args: Text, Var, bits, OnChange, Children
UIBitField = [
	uitoggle [ & $@arg2 @arg3 ] [
		if (& $@arg2 @arg3) [
			updateval @@arg2 (& $@@arg2 (~ @@arg3)) [@@@arg4]
		] [
			updateval @@arg2 (| $@@arg2 @@arg3) [@@@arg4]
		]
	] 0.5 [
		uialign -1 0
		uihlist 0.015 [
			uiimage "media/interface/checkbox_false_idle" 0.025 0.025
			uitext [@@@arg1]
		]
		uihlist 0.015 [
			uiimage "media/interface/checkbox_false_hover" 0.025 0.025
			uitext [@@@arg1]
		]
		uihlist 0.015 [
			uiimage "media/interface/checkbox_true_idle" 0.025 0.025
			uitext [@@@arg1]
		]
		uihlist 0.015 [
			uiimage "media/interface/checkbox_true_hover" 0.025 0.025
			uitext [@@@arg1]
		]
		@arg5
	]
]

//Similar to the above two items, but a radio button only has a true value
//args: Text, Var, Trueval, OnSelect, Children
UIRadioButton = [
	uitoggle [ =f $@arg2 @arg3 ] [
		updateval @arg2 @arg3 [@@arg4]
	] 0.5 [
		uihlist 0.015 [
			uiimage "media/interface/radio_false_idle" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/radio_false_hover" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/radio_true_idle" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/radio_true_hover" 0.025 0.025
			uitext [@@@arg1] 1
		]
        @arg5
	]
]

//Like above, but for strings
//args: Text, Var, Trueval, OnSelect, Children
UIRadioButtonGeneric = [
	uitoggle [ =s $@arg2 @(escape $arg3) ] [
		updateval @arg2 @(escape $arg3) [@@arg4]
	] 0.5 [
		uihlist 0.015 [
			uiimage "media/interface/radio_false_idle" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/radio_false_hover" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/radio_true_idle" 0.025 0.025
			uitext [@@@arg1] 1
		]
		uihlist 0.015 [
			uiimage "media/interface/radio_true_hover" 0.025 0.025
			uitext [@@@arg1] 1
		]
        @arg5
	]
]

//Creates an expandable box with more advanced contents
//args: Text, variable, Contents
UIExpandable = [
	uivlist 0 [
		uitoggle [ = $@@arg2 1 ] [
			updateval @@arg2 (! $@@arg2)
		] 0.5 [
			uialign -1 0
			uifill 0 0 [
				uihlist 0 [
					uiimage "<rotate:1>media/interface/arrow_hover" 0.025 0.025
					uioffset 0.015 0
					uitext [@@@@arg1] 1
				]
				UIIdle
			]
			uifill 0 0 [
				uihlist 0 [
					uiimage "<rotate:1>media/interface/arrow_hover" 0.025 0.025
					uioffset 0.015 0
					uitext [@@@@arg1] 1
				]
				UIHover
			]
			uifill 0 0 [
				uihlist 0 [
					uiimage "<rotate:2>media/interface/arrow_hover" 0.025 0.025
					uioffset 0.015 0
					uitext [@@@@arg1] 1
				]
				UIIdle
			]
			uifill 0 0 [
				uihlist 0 [
					uiimage "<rotate:2>media/interface/arrow_hover" 0.025 0.025
					uioffset 0.015 0
					uitext [@@@@arg1] 1
				]
				UIHover
			]
		]
		uicond [ = $@@arg2 1 ] [
			uistretchedimage "media/interface/expandable-bg" .02 .02 [
				uispace 0.01 0.01 [@@arg3]
			]
			uifill 0 0
		]
	]
]

//Creates a uiscroll area with a uihscrollbar at the bottom
//args: X-Size, Y-Size, ArrowSize, ScrollSpeed, Contents
UIHScroller = [
	local f
	flip = (<f $arg2 0)
	arg2 = (absf $arg2)

	uivlist 0 [
		if (! $f) [
			uiscroll [@@@arg1] (-f [@@@arg2] [@@@arg3]) [
				@@arg5
			]
		]
		uihscrollbar [@@arg3] [@@arg4] [
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uiimage "<rotate:3>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uiimage "<rotate:1>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uiimage "<rotate:3>media/interface/arrow_hover" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uiimage "<rotate:1>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uiimage "<rotate:3>media/interface/arrow_selected" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uiimage "<rotate:1>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uiimage "<rotate:3>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uiimage "<rotate:1>media/interface/arrow_hover" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg1] [@@@arg3] [
				uihlist 0 [
					uiimage "<rotate:3>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_horizontal" (-f [@@@@@arg2] (*f [@@@@@arg3] 2)) [@@@@@arg3]
					uiimage "<rotate:1>media/interface/arrow_selected" [@@@@@arg3] [@@@@@arg3]
				]
			]

			uiscrollbutton [
				uistretchedimage "media/interface/scrollbutton_idle" [@@@@arg3] [@@@@arg3] [uiclamp 1 1 0 0]
				uistretchedimage "media/interface/scrollbutton_hover" [@@@@arg3] [@@@@arg3] [uiclamp 1 1 0 0]
				uistretchedimage "media/interface/scrollbutton_selected" [@@@@arg3] [@@@@arg3] [uiclamp 1 1 0 0]
			]
		]
		if $f [
			uiscroll [@@@arg1] (-f [@@@arg2] [@@@arg3]) [
				@@arg5
			]
		]
	]
]

//Creates a uiscroll area with a uivscrollbar at the right
//args: X-Size, Y-Size, ArrowSize, ScrollSpeed, Contents
UIVScroller = [
	local f
	f = (<f $arg1 0)
	arg1 = (absf $arg1)

	uihlist 0 [
		if (! $f) [
			uiscroll (-f [@@@arg1] [@@@arg3]) [@@@arg2] [
				uifill (-f [@@@@arg1] [@@@@arg3]) [@@@@arg2]
				@@arg5
			]
		]
		uivscrollbar [@@arg3] [@@arg4] [
			uifill [@@@arg3] [@@arg2]  [
				uivlist 0 [
					uiimage "media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uiimage "<rotate:2>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uiimage "media/interface/arrow_hover" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uiimage "<rotate:2>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uiimage "media/interface/arrow_selected" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uiimage "<rotate:2>media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uiimage "media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uiimage "<rotate:2>media/interface/arrow_hover" [@@@@@arg3] [@@@@@arg3]
				]
			]
			uifill [@@@arg3] [@@arg2] [
				uivlist 0 [
					uiimage "media/interface/arrow_idle" [@@@@@arg3] [@@@@@arg3]
					uiimage "media/interface/bar_vertical" [@@@@@arg3] (-f [@@@@@arg2] (*f [@@@@@arg3] 2))
					uiimage "<rotate:2>media/interface/arrow_selected" [@@@@@arg3] [@@@@@arg3]
				]
			]

			uiscrollbutton [
				uistretchedimage "media/interface/scrollbutton_idle" [@@@arg3] [@@@arg3] [uiclamp 0 0 1 1]
				uistretchedimage "media/interface/scrollbutton_hover" [@@@arg3] [@@@arg3] [uiclamp 0 0 1 1]
				uistretchedimage "media/interface/scrollbutton_selected" [@@@arg3] [@@@arg3] [uiclamp 0 0 1 1]
			]
		]
		if $f [
			uiscroll (-f [@@@arg1] [@@@arg3]) [@@@arg2] [
				uifill (-f [@@@@arg1] [@@@@arg3]) [@@@@arg2]
				@@arg5
			]
		]
	]
]

//Creates a horizontal slider for adjusting a variable
//args: Var, Min, Max, OnChange, Length, ArrowSize, StepSize, StepTime, Children
UIHSlider = [
	uihslider [@arg1] [@arg2] [@arg3] [@arg4] [@arg6] [@arg7] [@arg8] [
		uifill [@@arg5] [@@arg6] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg5] (*f [@@@@arg6] 2)) [@@@@arg6]
				uiimage "<rotate:1>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg5] [@@arg6] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_hover" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg5] (*f [@@@@arg6] 2)) [@@@@arg6]
				uiimage "<rotate:1>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg5] [@@arg6] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_selected" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg5] (*f [@@@@arg6] 2)) [@@@@arg6]
				uiimage "<rotate:1>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg5] [@@arg6] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
				uistretchedimage "media/interface/bar_horizontal" (-f [@@@@arg5] (*f [@@@@arg6] 2)) [@@@@arg6]
				uiimage "<rotate:1>media/interface/arrow_hover" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg5] [@@arg6] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg5] (*f [@@@@arg6] 2)) [@@@@arg6]
				uiimage "<rotate:1>media/interface/arrow_selected" [@@@@arg6] [@@@@arg6]
			]
		]
		uisliderbutton [
			uialign 0 0
			uistretchedimage "media/interface/scrollbutton_idle" [@@@arg6] [@@@arg6] [
				uiclamp 1 1 0 0
				uispace 0.001 0 [
					uievaltext [result $@@@@@arg1] @@@@(minf 1 (*f $arg6 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_hover" [@@@arg6] [@@@arg6] [
				uiclamp 1 1 0 0
				uispace 0.001 0 [
					uievaltext [result $@@@@@arg1] @@@@(minf 1 (*f $arg6 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_selected" [@@@arg6] [@@@arg6] [
				uiclamp 1 1 0 0
				uispace 0.001 0 [
					uievaltext [result $@@@@@arg1] @@@@(minf 1 (*f $arg6 40))
				]
			]
		]
		@arg9
	]
]

//like the above, but vertical
//args: Var, Min, Max, OnChange, Length, ArrowSize, StepSize, StepTime,, Children
UIVSlider = [
	uivslider [@arg1] [@arg2] [@arg3] [@arg4] [@arg6] [@arg7] [@arg8] [
		uifill [@@arg6] [@@arg5] [
			uihlist 0 [
				uiimage "media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_vertical" [@@@@arg6] (-f [@@@@arg5] (*f [@@@@arg6] 2))
				uiimage "<rotate:2>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg5] [@@arg5] [
			uihlist 0 [
				uiimage "media/interface/arrow_hover" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_vertical" [@@@@arg6] (-f [@@@@arg5] (*f [@@@@arg6] 2))
				uiimage "<rotate:2>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg6] [@@arg5] [
			uihlist 0 [
				uiimage "media/interface/arrow_selected" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_vertical" [@@@@arg6] (-f [@@@@arg5] (*f [@@@@arg6] 2))
				uiimage "<rotate:2>media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg6] [@@arg5] [
			uihlist 0 [
				uiimage "media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_vertical" [@@@@arg6] (-f [@@@@arg4] (*f [@@@@arg6] 2))
				uiimage "<rotate:2>media/interface/arrow_hover" [@@@@arg6] [@@@@arg6]
			]
		]
		uifill [@@arg6] [@@ar@@@(min 0.025 (*f $arg5 40))g5] [
			uihlist 0 [
				uiimage "media/interface/arrow_idle" [@@@@arg6] [@@@@arg6]
				uiimage "media/interface/bar_vertical" [@@@@arg6] (-f [@@@@arg5] (*f [@@@@arg6] 2))
				uiimage "<rotate:2>media/interface/arrow_selected" [@@@@arg6] [@@@@arg6]
			]
		]

		uisliderbutton [
			uialign 0 0
			uistretchedimage "media/interface/scrollbutton_idle" [@@@arg6] [@@@arg6] [
				uiclamp 0 0 1 1
				uispace 0.001 0 [
					uievaltext [result $@@@@@arg1] @@@@(minf 1 (*f $arg6 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_hover" [@@@arg6] [@@@arg6] [
				uiclamp 0 0 1 1
				uispace 0.001 0 [
					uievaltext [result $@@@@@arg1] @@@@(minf 1 (*f $arg6 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_selected" [@@@arg6] [@@@arg6] [
				uiclamp 0 0 1 1
				uispace 0.001 0 [
					uievaltext [result $@@@@@arg1] @@@@(minf 1 (*f $arg6 40))
				]
			]
		]
		@arg9
	]
]

//a horizontal list slider.
//contains n elements and moves through the list as opposed to two ranges
//args: Var, List, OnChange, Length, ArrowSize, Children
UIHListSlider = [
	[listslider@arg1] = (indexof $arg2 $$arg1)
	uihslider [listslider@arg1] 0 (- (listlen $arg2) 1) [
		updateval [@@arg1] (at [@@arg2] $[listslider@@arg1]) [@@arg3]
	] [@arg5] [1] [500] [
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uiimage "<rotate:1>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_hover" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uiimage "<rotate:1>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_selected" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uiimage "<rotate:1>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uiimage "<rotate:1>media/interface/arrow_hover" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg4] [@@arg5] [
			uihlist 0 [
				uiimage "<rotate:3>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_horizontal" (-f [@@@@arg4] (*f [@@@@arg5] 2)) [@@@@arg5]
				uiimage "<rotate:1>media/interface/arrow_selected" [@@@@arg5] [@@@@arg5]
			]
		]
		uisliderbutton [
			uialign 0 0
			uistretchedimage "media/interface/scrollbutton_idle" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uispace 0.001 0 [
					uievaltext [at [@@@@@@arg2] $[listslider@@@@@@arg1]] @@@@(minf 1 (*f $arg5 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_hover" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uispace 0.001 0 [
					uievaltext [at [@@@@@@arg2] $[listslider@@@@@@arg1]] @@@@(minf 1 (*f $arg5 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_selected" [@@@arg5] [@@@arg5] [
				uiclamp 1 1 0 0
				uispace 0.001 0 [
					uievaltext [at [@@@@@@arg2] $[listslider@@@@@@arg1]] @@@@(minf 1 (*f $arg5 40))
				]
			]
		]
		@arg7
	]
]

//like the above, but vertical
//args: Var, List, OnChange, Length, ArrowSize, Children
UIVListSlider = [
	[listslider@arg1] = (indexof $arg2 $$arg1)
	uivslider [listslider@arg1] 0 (- (listlen $arg2) 1) [
		updateval [@@arg1] (at [@@arg2] $[listslider@@arg1]) [@@arg3]
	] [@arg5] [1] [500] [
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uiimage "media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uiimage "<rotate:2>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uiimage "media/interface/arrow_hover" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uiimage "<rotate:2>media/interface/arrow_idle"  [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uiimage "media/interface/arrow_selected" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uiimage "<rotate:2>media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uiimage "media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uiimage "<rotate:2>media/interface/arrow_hover" [@@@@arg5] [@@@@arg5]
			]
		]
		uifill [@@arg5] [@@arg4] [
			uihlist 0 [
				uiimage "media/interface/arrow_idle" [@@@@arg5] [@@@@arg5]
				uiimage "media/interface/bar_vertical" [@@@@arg5] (-f [@@@@arg4] (*f [@@@@arg5] 2))
				uiimage "<rotate:2>media/interface/arrow_selected"  [@@@@arg5] [@@@@arg5]
			]
		]

		uisliderbutton [
			uialign 0 0
			uistretchedimage "media/interface/scrollbutton_idle" [@@@arg5] [@@@arg5] [
				uiclamp 0 0 1 1
				uispace 0.001 0 [
					uievaltext [at [@@@@@@arg2] $[listslider@@@@@@arg1]] @@@@(minf 1 (*f $arg5 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_hover" [@@@arg5] [@@@arg5] [
				uiclamp 0 0 1 1
				uispace 0.001 0 [
					uievaltext [at [@@@@@@arg2] $[listslider@@@@@@arg1]] @@@@(minf 1 (*f $arg5 40))
				]
			]
			uistretchedimage "media/interface/scrollbutton_selected" [@@@arg5] [@@@arg5] [
				uiclamp 0 0 1 1
				uispace 0.001 0 [
					uievaltext [at [@@@@@@arg2] $[listslider@@@@@@arg1]] @@@@(minf 1 (*f $arg5 40))
				]
			]
		]
		@arg7
	]
]

// Creatres a solid border, also acts as a background.
// ideal for image-based buttons
// args: contents, children
UIBorder = [
	uicolor 0x5F3F5F8F 0 0 [
		uispace 0.005 0.005 [
			@@arg1
		]
		if (>= $numargs 2) [
			@@arg2
		]
	]
]

//Creates a simple field for text input.
//The background is a transparent orange to signify the area
//args; Variable, length, onchange, scale, filter, children
UIField = [
	uistretchedimage "media/interface/field" .02 .02 [
		uifield [@@arg1] [@@arg2] [@@arg3] [@@arg4] [@@arg5]
		if (>= $numargs 6) [
			@@arg6
		]
	]
]

//Creates a generic Text Editor widget
//the background is slightly purple
//args: name, width, height, scale, initial value, keep, filter, children
UITextEditor = [
	uivlist 0 [
		uistretchedimage "media/interface/editor" .02 .02 [
			uialign 0 0
			uitexteditor [@@@arg1] [@@@arg2] [@@@arg3] [@@@arg4] [@@@arg5] [@@@arg6]
		]
		uihlist 0.02 [
            uialign -1 -1
			UIBasicButton [
				uitext "Load"
			] [
				textfocus [@@@@arg1]
				textload [@@@@arg1]
			]
			UIBasicButton [
				uitext "Save"
			] [
				textfocus [@@@@arg1]
				textsave [@@@@arg1]
			]
			UIBasicButton [
				uitext "Exec"
			] [
				textfocus [@@@@arg1]
				textexec (= (textmark 0) 1)
			]
			UIBasicButton [
				uitext "Copy"
			] [
				textfocus [@@@@arg1]
				textcopy
			]
			UIBasicButton [
				uitext "Paste"
			] [
				textfocus [@@@@arg1]
				textpaste
			]
			UIBasicButton [
				uitext "Select"
			] [
				textfocus [@@@@arg1]
				textselectall
			]
			UIBasicButton [
				uitext "Clear"
			] [
				textfocus [@@@@arg1]
				textclear
			]
		]
		@@arg7
	]
]

//some example filters
UINumberFilter = "-0xX123456789aAbBcCdDeEfF"
UIPositiveDecimalFilter = "0123456789"
UIFloatFilter = "-0123456789.eEfF"
UISafeFilter = " 0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ.-_~/\"
