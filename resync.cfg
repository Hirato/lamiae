do [
	local oldmodellist newmodellist oldtexturelist newtexturelist
// 	oldtexturelist = []; newtexturelist = []

	oldmodellist = (loopconcat i (nummapmodels) [ mapmodelname $i ])

	//0 shader
	//1 shaderparams
	//2 textures
	//3 autograss
	//4/5 scroll
	//6/7 offset
	//8 rotate
	//9 scale
	//10 layer
	//11 decal
	//12/13 alpha
	//14/15/16 color
	//17/18/19/20 refract
	local res
	oldtexturelist = (loopconcat i (numslots) [
		concat "[" (gettexshader $i) "[" (
				res = []
				loopshaderparams $i param val [ res = (concat $res (escape $param) "[" $val "]") ]
				result $res
			) "] [" (
				res = []
				looptexslots $i type tex [ res = (concat $res $type (escape $tex)) ]
				result $res
			) "]" (escape (gettexautograss $i)) (gettexscroll $i) (gettexoffset $i) (gettexrotate $i) (gettexscale $i) (gettexlayer $i) (gettexdecal $i) (gettexalpha $i) (gettexcolor $i) (gettexrefract $i) "]^n"
	])

	//reset the texture/model lists
	exec config/default_map_settings.cfg

	newmodellist = (loopconcat i (nummapmodels) [ mapmodelname $i ])
	newmodelist = (listdel $newmodellist $oldmodellist)

	mapmodelreset

	looplist model $oldmodellist [ mmodel $model ]
	//this weird loop is meant to prevent the new list from inserting anything twice.
	while [ !=s (at $newmodellist 0) "" ] [ mmodel (at $newmodellist 0); newmodellist = (listdel $newmodellist (at $newmodellist 0)) ]


	newtexturelist = (loopconcat i (numslots) [
		concat "[" (gettexshader $i) "[" (
				res = []
				loopshaderparams $i param val [ res = (concat $res (escape $param) "[" $val "]") ]
				result $res
			) "] [" (
				res = []
				looptexslots $i type tex [ res = (concat $res $type (escape $tex)) ]
				result $res
			) "]" (escape (gettexautograss $i)) (gettexscroll $i) (gettexoffset $i) (gettexrotate $i) (gettexscale $i) (gettexlayer $i) (gettexdecal $i) (gettexalpha $i) (gettexcolor $i) (gettexrefract $i) "]^n"
	])
	newtexturelist = (listdel $newtexturelist $oldtexturelist)

	texturereset
	looplist tex $oldtexturelist [
		setshader (at $tex 0)
		loop i (div (listlen (at $tex 1)) 2) [
			do [setshaderparam (at $tex 1 (* $i 2)) @(at $tex 1 (+ (* $i 2) 1)) ]
		]

		loop i (div (listlen (at $tex 2)) 2) [
			texture (at $tex 2 (* $i 2)) (at $tex 2 (+ (* $i 2) 1))
		]

		if (at $tex 3) [ autograss (at $tex 3) ]
		texscroll (at $tex 4) (at $tex 5)
		texoffset (at $tex 6) (at $tex 7)
		texrotate (at $tex 8)
		texscale (at $tex 9)
		texlayer (at $tex 10)
		texdecal (at $tex 11)
		texalpha (at $tex 12) (at $tex 13)
		texcolor (at $tex 14) (at $tex 15) (at $tex 16)
		texrefract (at $tex 17) (at $tex 18) (at $tex 19) (at $tex 20)
	]

	while [ !=s (at $newtexturelist 0) "" ] [
		//note that listdel takes a list
		//we use concatword to assure ti gets treated as a single element.
		local tex
		tex = (at $newtexturelist 0)
		newtexturelist = (listdel $newtexturelist (concatword "[" $tex "]"))

		setshader (at $tex 0)
		loop i (div (listlen (at $tex 1)) 2) [
			do [setshaderparam (at $tex 1 (* $i 2)) @(at $tex 1 (+ (* $i 2) 1)) ]
		]

		loop i (div (listlen (at $tex 2)) 2) [
			texture (at $tex 2 (* $i 2)) (at $tex 2 (+ (* $i 2) 1))
		]

		if (at $tex 3) [ autograss (at $tex 3) ]
		texscroll (at $tex 4) (at $tex 5)
		texoffset (at $tex 6) (at $tex 7)
		texrotate (at $tex 8)
		texscale (at $tex 9)
		texlayer (at $tex 10)
		texdecal (at $tex 11)
		texalpha (at $tex 12) (at $tex 13)
		texcolor (at $tex 14) (at $tex 15) (at $tex 16)
		texrefract (at $tex 17) (at $tex 18) (at $tex 19) (at $tex 20)
	]
]
