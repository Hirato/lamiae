loop i 3 [
	[panell@[i]selected] = 0
	[panelr@[i]selected] = 0
]

panell0 = []
panell1 = []
panell2 = [stats]
panelr0 = [sky lighting]
panelr1 = [material entities mapmodel]
panelr2 = []

paneltools = []

// alias should take the form showeditpanel@[alias].
// icon should be given relative to media/interface
// name is a very small description for the configuration menu
// Please use this template to create your own,
// You have .28 horizontal units to play with.

// 	showeditpanel@[alias] = [
// 		replaceui $arg1 content [
//			//content
// 		]
// 	]
//	registertool alias myicon "empty item"

registertool = [
	if (>= (gettool $arg1) 0) [
		error editpanel tool $arg1 already registered
	] [
		paneltools = (concat $paneltools "[" (escape $arg1) (escape @arg2) (escape $arg3) "]")
	]
]

gettool = [
	result (listfind t $paneltools [=s $arg1 (at $t 0)])
]

hideeditpanels = [
	loop i 3 [
		hideui [panell@[i]]
		hideui [panelr@[i]]
	]
]

showeditpanels = [
	local l r
	loop i 3 [
		if $[panell@[i]] [ l = (concat $l l@[i]) ]
		if $[panelr@[i]] [ r = (concat $r r@[i]) ]
	]

	loop i (listlen $l) [
		showpanel (at $l $i) $i @(listlen $l)
	]
	loop i (listlen $r) [
		showpanel (at $r $i) $i @(listlen $r)
	]
]

//args: varname, idx, number
showpanel = [
	local l len statsonly
	l = (>= (strstr $arg1 "l") 0)
	len = (listlen $[panel@[arg1]])

	if (&& (= $len 1) [=s (at $[panel@[arg1]] 0) "stats"]) [
		statsonly = 1
	]

	showui [panel@[arg1]] [
		case (? (&& (= $arg3 1) $statsonly) 3 $arg3) 1 [
			uialign (? $l -1 1) 0
		] 2 [
			uialign (? $l -1 1) (? $arg2 1 -1)
		] 3 [
			uialign (? $l -1 1) (+ -1 $arg2)
		]

		uivlist 0 [
			uihlist 0 [
				if (&& (> $len 1) $l) [
					createtoolbox $arg1
				]
				uistretchedimage "media/interface/body_full" 0.04 0.04 [
					if $statsonly [
						uispace 0.01 0.01 [
							uiclip .28 .3 [
								uifill .28 0
								uitag content []
							]
						]
					] [
						uispace 0.01 0.01 [
							UIVScroller (? $l 0.305 -0.305) (at ".9 .45 .3" (- $arg3 1)) .02 (divf 1 $arg3) [
								uioffset 0.005 0 [
									uiclamp 1 1 1 1
									uialign -1 -1
									uitag content []
								]
							]
						]
					]
				]
				if (&& (> $len 1) (! $l)) [
					createtoolbox $arg1
				]
			]
			if $l [ uifill 0 .05 ]
		]
	] () $statsonly

	[showeditpanel@(at $[panel@[arg1]] $[panel@[arg1]selected])] [panel@[arg1]]
]

//args: varname
createtoolbox = [
	uivlist 0.005 [
		uialign 0 -1
		loop i (listlen $panel@[arg1]) [
			local tool
			tool = (at $paneltools (gettool (at $panel@[arg1] $i)))
			if $tool [
				uitoggle [ = $[panel@@[arg1]selected] @i ] [
					[panel@@[arg1]selected] = @i
					[showeditpanel@@(at $tool 0)] [panel@@@[arg1]]
				] .5 [
					uistretchedimage "media/interface/body_full" 0.04 0.04
					uistretchedimage "media/interface/field" 0.04 0.04
					uistretchedimage "media/interface/expandable-bg" 0.04 0.04
					uistretchedimage "media/interface/field" 0.04 0.04
					uiimage (concatword "media/interface/" (at $tool 1)) .035 .035 [ uialign 0 0 ]
				]
			]
		]
	]
]

loopfiles f config/menus/editpanels cfg [
	exec (concatword "config/menus/editpanels/" $f .cfg)
]
