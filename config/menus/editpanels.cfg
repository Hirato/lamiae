loop i 3 [
	[panell@[i]selected] = 0
	[panelr@[i]selected] = 0
]

panell0 = []
panell1 = []
panell2 = []
panelr0 = []
panelr1 = [sky]
panelr2 = []

paneltools = []

// alias should take the form showeditpanel@[alias].
// It should open with uitag $arg1 content []
// You have .28 horizontal units to play with
// the icon sohuld be given relative to media/interface
// alias, icon, name
registertool = [
	if (>= (gettool $arg1) 0) [
		error editpanel tool $arg1 already registered
	] [
		paneltools = (concat paneltools "[" (escape $arg1) (escape @arg2) (escape $arg3) "]")
	]
]

gettool = [
	result (listfind t $paneltools [=s $arg1 (at $t 0)])
]

hideeditpanels = [
	loop i 3 [
		hideui [panell@[i]]
		hideui [panelr@[i]]
	]
]

showeditpanels = [
	local l r
	loop i 3 [
		if $[panell@[i]] [ l = (concat $l l@[i]) ]
		if $[panelr@[i]] [ r = (concat $r r@[i]) ]
	]

	loop i (listlen $l) [
		showpanel (at $l $i) $i @(listlen $l)
	]
	loop i (listlen $r) [
		showpanel (at $r $i) $i @(listlen $r)
	]
]

//args: varname, idx, number
showpanel = [
	local l
	l = (>= (strstr $arg1 "l") 0)

	showui [panel@[arg1]] [
		case $arg3 1 [
			uialign (? $l -1 1) 0
		] 2 [
			uialign (? $l -1 1) (? $arg2 1 -1)
		] 3 [
			uialign (? $l -1 1) (- 1 $arg2)
		]

		uihlist 0 [
			if $l [
				createtoolbox $arg1
			]
			uistretchedimage "media/interface/body_full" 0.04 0.04 [
				uispace 0.01 0.01 [
					UIVScroller (? $l 0.3 -0.3) (at ".9 .45 .3" (- $arg3 1)) .02 (divf 1 $arg3) [
						uitag content []
					]
				]
			]
			if (! $l) [
				createtoolbox $arg1
			]
		]
	]

	[panel@[arg1]selected] = 0
	[showeditpanel@(at $[panel@[arg1]] 0)] [panel@[arg1]]
]

//args: varname
createtoolbox = [
	uivlist 0.005 [
		uialign 0 -1
		loop i (listlen $panel@[arg1]) [
			local tool
			tool = (at $paneltools (gettool (at $panel@[arg1] $i)))
			if $tool [
				uitoggle [ = $[panel@@[arg1]selected] @i ] [
					[panel@@[arg1]selected] = @i
					[showeditpanel@@(at $tool 0)] [panel@@@[arg1]]
				] .5 [
					uistretchedimage "media/interface/body_full" 0.04 0.04
					uistretchedimage "media/interface/field" 0.04 0.04
					uistretchedimage "media/interface/expandable-bg" 0.04 0.04
					uistretchedimage "media/interface/field" 0.04 0.04
					uiimage (concatword "media/interface/" (at $tool 1)) .035 .035 [ uialign 0 0 ]
				]
			]
		]
	]
]

loopfiles f config/menus/editpanels cfg [
	exec (concatword "config/menus/editpanels/" $f .cfg)
]
